//Конфигурация для платформы "1С:Предприятие 7.7" РБ-Софт:Рабочее место кассира.
//Версия 4.57 от 05.06.11 23:24:52
//(c) Баинов Евгений, ООО "РБ-Софт" 2006-2011 г.Улан-Удэ, ул.Балтахинова,17Е,офис 215
//e-mail: bainov@rb-soft.ru  icq: 73718937, телефоны: (3012) 440-357, 278-228
//Сайт проекта: http://code.google.com/p/rbsoft-rmk/
//_______________________________________________________________________________________
//Экранная форма класса Клавиатура
//Имя формы : ОсновнаяФорма
//_______________________________________________________________________________________
Перем Объект;
Перем ТЗКоманды;
Перем ТабличноеПоле;
Перем ПД_Команды;
Перем ПоКнопке;
Перем Страница;

Перем ПолеТабличногоДокумента;


Процедура ЗаполнитьДанные()

	НастройкиКлавиатуры=Объект.НастройкиКлавиатуры;
	НастройкиКлавиатуры.ВыбратьСвойства();
	СсылкаНаСекцию="";
	ИмяСекции="";
	Пока НастройкиКлавиатуры.ПолучитьСвойство(СсылкаНаСекцию,ИмяСекции)=1 Цикл
		Если ИмяСекции="KEYTABLE" Тогда

			СсылкаНаСекцию.ВыбратьСвойства();
			СсылканаПоле="";
			ИмяПоля="";
			Пока СсылкаНаСекцию.ПолучитьСвойство(СсылканаПоле,ИмяПоля)=1 Цикл
				Если ИмяПоля="_Комментарий" Тогда
					Продолжить;
				ИначеЕсли ИмяПоля="_ИД" Тогда
					Продолжить;
				КонецЕсли;

				ТЗКоманды.НоваяСтрока();
				ТЗКоманды.Код = Число(СсылканаПоле.Значение);
				ТЗКоманды.ПредставлениеКнопки = Объект.ПредставлениеКнопки(ТЗКоманды.Код);
				ТЗКоманды.Команда = ИмяПоля;
				ТЗКоманды.Описание = СсылканаПоле._Комментарий;
			КонецЦикла;
		КонецЕсли;  //
	КонецЦикла;

КонецПроцедуры


Процедура ОбновитьТаблицу()

	ПолеТабличногоДокумента.Таблица.ИсходнаяТаблица("Клавиатура");
	ПолеТабличногоДокумента.Таблица.Очистить();
	ПолеТабличногоДокумента.Таблица.ВывестиСекцию("Шапка");

	ТЗКоманды.ВыбратьСтроки();
	Пока ТЗКоманды.ПолучитьСтроку() = 1 Цикл
		НомПП=ТЗКоманды.НомерСтроки;
		НаименованиеКоманды=ТЗКоманды.Команда;
		СканКод=ТЗКоманды.Код;
		КомбинацияКлавиш=Объект.ПредставлениеКнопки(ТЗКоманды.Код);
		ПолеТабличногоДокумента.Таблица.ВывестиСекцию("Строка");
	КонецЦикла;
	ПолеТабличногоДокумента.Таблица.ТолькоПросмотр(1);
	ПолеТабличногоДокумента.Таблица.ПараметрыСтраницы(1,,,10,0,0,0,0,0,1);

	ПолеТабличногоДокумента.Менеджер.ВидимостьПолосПрокрутки(1);
	ПолеТабличногоДокумента.Менеджер.ВыводСетки=0;
	ПолеТабличногоДокумента.Менеджер.ВыводЗаголовков=0;
	ПолеТабличногоДокумента.Менеджер.Масштаб(120);

КонецПроцедуры


Процедура СменитьСтраницу()

	Если Страница="Основной" Тогда
		Страница="Таблица";

		Форма.кнЗаписать.Доступность(0);
		Форма.кнОчистить.Доступность(0);
		Форма.кнПечатать.Видимость(1);
		Форма.кнСтраница.Заголовок("Назад");
		ОбновитьТаблицу();
	Иначе
		Страница="Основной";

		Форма.кнЗаписать.Доступность(1);
		Форма.кнОчистить.Доступность(1);
		Форма.кнПечатать.Видимость(0);
		Форма.кнСтраница.Заголовок("Справка");
	КонецЕсли;
	Форма.ИспользоватьСлой(Страница+",Подвал",2);



КонецПроцедуры

Процедура ПослеОткрытия()

	ЗаполнитьДанные();

	ПД_Команды=СоздатьОбъект("ПоставщикДанныхТЗ");
	ПД_Команды.УстТаблицуЗначений(ТЗКоманды);

	ТабличноеПоле=СоздатьОбъект("ТабличноеПолеКлавиатура");
	ТабличноеПоле.Инит(Контекст,"ТП_Команды",ПД_Команды);
    ТабличноеПоле.Клавиатура=Объект;


	ТабличноеПоле.ОбновитьСтроки();
	Форма.Обновить();

	Активизировать("ТП_Команды");

	оФорма = СоздатьОбъект("Форма");
	оФорма.УстановитьФорму(Форма);


	ПолеТабличногоДокумента=оФорма.СоздатьЭлементУправления("ПолеТабличногоДокумента",Форма.ПолеТаблицы);

	Страница="";
	СменитьСтраницу();

КонецПроцедуры





Процедура ПриОткрытии()

	Если ПустоеЗначение(Форма.Параметр)=1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	СтруктураПараметров=Форма.Параметр;
	Объект=СтруктураПараметров.Объект;

КонецПроцедуры	// ПриОткрытии


Процедура ПриПовторномОткрытии()
	ПриОткрытии();
КонецПроцедуры	// ПриПовторномОткрытии





Процедура Записать()

	Если глВопрос("Записать текущую раскладку клавиатуры?","Да+Нет")<>"Да" Тогда
		Возврат;
	КонецЕсли;  //
	ТЗКоманды.ВыбратьСтроки();
	Пока ТЗКоманды.ПолучитьСтроку() = 1 Цикл
		Объект.НастройкиКлавиатуры.Установить("KEYTABLE/"+ТЗКоманды.Команда,ТЗКоманды.Код);
	КонецЦикла;
	Объект.ИнициализироватьИзНастроек();

	Если Объект.Записать()=0 Тогда
	    глПредупреждение("Не удалось записать раскладку.
		|"+Объект.ПоследняяОшибка());
	КонецЕсли;  //

КонецПроцедуры

Процедура ОчиститьТекущуюКоманду()
	ТЗКоманды.УстановитьЗначение(ТабличноеПоле.ТекущаяСтрока,"Код",0);
	ТЗКоманды.УстановитьЗначение(ТабличноеПоле.ТекущаяСтрока,"ПредставлениеКнопки","");
	ТабличноеПоле.ПроверитьДубли();
	Активизировать("ТП_Команды");
	Форма.Обновить();
КонецПроцедуры

Процедура ПриЗакрытии()
	Если ПоКнопке=0 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры


Процедура Печатать()
	ПолеТабличногоДокумента.Таблица.Напечатать(1);
КонецПроцедуры

ТЗКоманды = СоздатьОбъект("ТаблицаЗначений");
ТЗКоманды.НоваяКолонка("Статус","Число");
ТЗКоманды.НоваяКолонка("Код");
ТЗКоманды.НоваяКолонка("ПредставлениеКнопки","Строка");
ТЗКоманды.НоваяКолонка("Команда","Строка");
ТЗКоманды.НоваяКолонка("Описание","Строка");
ПоКнопке=0;
