//Конфигурация для платформы "1С:Предприятие 7.7" РБ-Софт:Рабочее место кассира.
//Версия 4.57 от 05.06.11 23:24:52
//(c) Баинов Евгений, ООО "РБ-Софт" 2006-2011 г.Улан-Удэ, ул.Балтахинова,17Е,офис 215
//e-mail: bainov@rb-soft.ru  icq: 73718937, телефоны: (3012) 440-357, 278-228
//Сайт проекта: http://code.google.com/p/rbsoft-rmk/
//_______________________________________________________________________________________
//Класс Устройство.ККМ
//_______________________________________________________________________________________
Перем _База Экспорт;
Перем КонтрольнаяЛента Экспорт; //Класс для регистрации всех операций печати на ККМ

Функция Этот(Конт) Возврат Конт; КонецФункции
Функция Сам() Возврат Этот(Контекст) КонецФункции



Функция ТипУстройства() Экспорт
	возврат "ККМ";
КонецФункции	// ТипУстройства


//------------------------------------ ------------------------------------
Процедура Конструктор()
	_База = Этот(Контекст).ПолучитьБазовыйКласс("Устройство");
	КонтрольнаяЛента=СоздатьОбъект("КонтрольнаяЛента");

КонецПроцедуры // Конструктор



//------------------------------------ ------------------------------------
Процедура Деструктор()
КонецПроцедуры // Деструктор

Функция ЕстьДанныеНаВходе(Источник,Событие,Данные) Экспорт
	возврат 0;
КонецФункции

//------------------------------------ ------------------------------------
Процедура ПриОткрытии()

	Форма.Параметр._ПриОткрытии(); // для отладки класса

КонецПроцедуры // ПриОткрытии



Функция ПробитьЧек(Чек) Экспорт
	Сам=Сам();
	Если Чек.Данные.КодОперации=0 Тогда
		глПослатьКоманду(Сам,"ЗаписатьЛогНачалоЧека","Продажа",,Сам);
	ИначеЕсли Чек.Данные.КодОперации=1 Тогда
		глПослатьКоманду(Сам,"ЗаписатьЛогНачалоЧека","Возврат",,Сам);
	Иначе
		глПослатьКоманду(Сам,"ЗаписатьЛогНачалоЧека",Строка(Чек.Данные.КодОперации),,Сам);
	КонецЕсли;


	ТекстЧека="";
	Рез= Сам.ИнтерфейсОборудования.ПробитьЧек(Чек,ТекстЧека);


	глПослатьКоманду(Сам,"ЗаписатьКонтрольнуюЛенту","<Text>
	|"+ТекстЧека+"
	|</Text>",,Сам);


	Если Рез=0 Тогда
		ТекстОшибки=Сам.ИнтерфейсОборудования.ПоследняяОшибка();
		глПослатьКоманду(Сам,"ЗаписатьЛогЧекНеЗакрыт",ТекстОшибки,,Сам);
		Сам.УстановитьОшибку(ТекстОшибки);
	Иначе
		глПослатьКоманду(Сам,"ЗаписатьЛогЧекЗакрыт",,,Сам);
		Сам.СброситьОшибку();
	КонецЕсли;

	Возврат Рез;
КонецФункции

//ТекстовыйМакет - Индексированная таблица с описанием текста
Функция НапечататьТекст(ТекстовыйМакет) экспорт

	Сам=Сам();

	ТекстовыйРезультат="";
	Рез= Сам.ИнтерфейсОборудования.НапечататьТекст(ТекстовыйМакет,ТекстовыйРезультат);

	глПослатьКоманду(Сам,"ЗаписатьКонтрольнуюЛенту","<Text>
	|"+ТекстовыйРезультат+"
	|</Text>",,Сам);


	Если Рез=0 Тогда
		Сам.УстановитьОшибку(Сам.ИнтерфейсОборудования.ПоследняяОшибка());
	Иначе
		Сам.СброситьОшибку();
	КонецЕсли;
	Возврат Рез;
КонецФункции


Функция СоздатьИнтерфейсОборудования() Экспорт
	Сам=Сам();

	Попытка
		лИмяДрайвера=СокрЛП(Сам.Данные.Драйвер);
		лИмяИнтерфейса="ИнтерфейсОборудования.ККМ."+лИмяДрайвера;
		Интерфейс=СоздатьОбъект(лИмяИнтерфейса);
		Если Интерфейс.ПривязатьДрайвер(Сам.Данные.Драйвер)=0 Тогда
			глДебаг("Драйвер не привязан к интерфейсу","Устройство.СоздатьИнтерфейсОборудования()");
			Возврат 0;
		КонецЕсли;
		возврат Интерфейс;
	Исключение
		глДебаг("ИмяИнтерфейса="+лИмяИнтерфейса+"
		|"+ОписаниеОшибки(),"Устройство.СоздатьИнтерфейсОборудования()");
	КонецПопытки;
	Возврат 0;
КонецФункции


//ТипОтрезки - 	0 -отрезать полностью
//				1 - неполностью
Функция ОтрезатьЛенту(ТипОтрезки=0) Экспорт
	Сам=Сам();
	Рез= Сам.ИнтерфейсОборудования.ОтрезатьЛенту(ТипОтрезки);

	Если Рез=0 Тогда
		Сам.УстановитьОшибку(Сам.ИнтерфейсОборудования.ПоследняяОшибка());
	Иначе
		Сам.СброситьОшибку();
	КонецЕсли;
	Возврат Рез;

КонецФункции

Функция ЗакрытьСмену() Экспорт
	Сам=Сам();

	ПараметрыСмены=Сам.ИнтерфейсОборудования.ПолучитьПараметрыСмены();

	СобытиеКоманда=глПослатьКоманду(Сам,"ЗаписатьЗакрытиеСмены",ПараметрыСмены,"БД");
	Если СобытиеКоманда.Обработано=0 Тогда
		глПредупреждение("Запись в базу данных о закрытии смены не произведена.");
		Возврат 0;
	Иначе
		Если СобытиеКоманда.Описатель.Ответ.Результат=0 Тогда
			глПредупреждение("Запись в базу данных о закрытии смены не произведена.");
			Возврат 0;
		КонецЕсли;
	КонецЕсли;

	Рез= Сам.ИнтерфейсОборудования.ЗакрытьСмену();
	Если Рез=0 Тогда
		Сам.УстановитьОшибку(Сам.ИнтерфейсОборудования.ПоследняяОшибка());
	Иначе
		Сам.СброситьОшибку();

		Документ=СобытиеКоманда.Описатель.Ответ.Документ;
		Документ.Статус=1;
		Документ.НомерКассовогоЧека=Сам.ПолучитьСквознойНомерДокумента();
		Если глБД.ЗаписатьОбъект(Документ)=0 Тогда
			глПредупреждение("Запись в базу данных о закрытии смены не произведена.");
		КонецЕсли;
	КонецЕсли;
	Возврат Рез;
КонецФункции

Функция ОтчетБезГашения() Экспорт

	Сам=Сам();


	ПараметрыСмены=Сам.ИнтерфейсОборудования.ПолучитьПараметрыСмены();

	Если ПустоеЗначение(ПараметрыСмены)=0 Тогда
		СобытиеКоманда=глПослатьКоманду(Сам,"ЗаписатьОтчетБезГашения",ПараметрыСмены,"БД");

		Если СобытиеКоманда.Обработано=0 Тогда
			глПредупреждение("Запись в базу данных о закрытии смены не произведена.");
			Возврат 0;
		Иначе
			Если СобытиеКоманда.Описатель.Ответ.Результат=0 Тогда
				глПредупреждение("Запись в базу данных о закрытии смены не произведена.");
				Возврат 0;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;

	Рез= Сам.ИнтерфейсОборудования.ОтчетБезГашения();

	////////
	Если Рез=0 Тогда
		Сам.УстановитьОшибку(Сам.ИнтерфейсОборудования.ПоследняяОшибка());
	Иначе
		Сам.СброситьОшибку();

		Документ=СобытиеКоманда.Описатель.Ответ.Документ;
		Документ.Статус=0;
		Документ.НомерКассовогоЧека=Сам.ПолучитьСквознойНомерДокумента();
		Если глБД.ЗаписатьОбъект(Документ)=0 Тогда
			глПредупреждение("Запись в базу данных о закрытии смены не произведена.");
		КонецЕсли;
	КонецЕсли;
	;
	Возврат Рез;
КонецФункции

Функция Внесение(СуммаВнесения) Экспорт
	Сам=Сам();

	глПослатьКоманду(Сам,"ЗаписатьЛогНачалоЧека","Внесение Сумма="+СуммаВнесения,,Сам);
	Рез= Сам.ИнтерфейсОборудования.Внесение(СуммаВнесения);


	Если Рез=0 Тогда
		ТекстОшибки=Сам.ИнтерфейсОборудования.ПоследняяОшибка();
		Сам.УстановитьОшибку(ТекстОшибки);

		глПослатьКоманду(Сам,"ЗаписатьЛогЧекНеЗакрыт",ТекстОшибки,,Сам);
	Иначе

		Сам.СброситьОшибку();

		глПослатьКоманду(Сам,"ЗаписатьЛогЧекЗакрыт",,,Сам);
	КонецЕсли;
	Возврат Рез;
КонецФункции

Функция Выплата(СуммаВыплаты,ВидОперации="") Экспорт

	Сам=Сам();
	глПослатьКоманду(Сам,"ЗаписатьЛогНачалоЧека","Выплата Сумма="+СуммаВыплаты+?(ВидОперации="",""," ВидОперации="+ВидОперации),,Сам);
	Рез= Сам.ИнтерфейсОборудования.Выплата(СуммаВыплаты);


	Если Рез=0 Тогда
		ТекстОшибки=Сам.ИнтерфейсОборудования.ПоследняяОшибка();
		Сам.УстановитьОшибку(ТекстОшибки);

		глПослатьКоманду(Сам,"ЗаписатьЛогЧекНеЗакрыт",ТекстОшибки,,Сам);
	Иначе
		Сам.СброситьОшибку();

		глПослатьКоманду(Сам,"ЗаписатьЛогЧекЗакрыт",,,Сам);
	КонецЕсли;
	Возврат Рез;
КонецФункции



//возвращает сумму денег в денежном ящике
//если произошел сбой - выставляется код ошибки
Функция СуммаНаличности() Экспорт
	Сам=Сам();
	Попытка
		возврат Сам.ИнтерфейсОборудования.СуммаНаличности();
	Исключение
		Сам.УстановитьОшибку("Ошибка при определении суммы наличности:"+ОписаниеОшибки());
		Возврат 0;
	КонецПопытки;

КонецФункции


//абстрактный метод.переопределять обязательно
Функция ПолучитьСквознойНомерДокумента() Экспорт
	возврат Сам().ИнтерфейсОборудования.ПолучитьСквознойНомерДокумента();
КонецФункции

Функция ПолучитьНомерПродажи() Экспорт
	возврат Сам().ИнтерфейсОборудования.ПолучитьНомерПродажи();
КонецФункции

Функция ПолучитьНомерВозвратаПродажи() Экспорт
	возврат Сам().ИнтерфейсОборудования.ПолучитьНомерВозвратаПродажи();
КонецФункции

Функция ПолучитьНомерПокупки() Экспорт
	возврат Сам().ИнтерфейсОборудования.ПолучитьНомерПокупки();
КонецФункции

Функция ПолучитьНомерВозвратаПокупки() Экспорт
	возврат Сам().ИнтерфейсОборудования.ПолучитьНомерВозвратаПокупки();
КонецФункции


Функция ПолучитьНомерСмены() Экспорт
	возврат Сам().ИнтерфейсОборудования.ПолучитьНомерСмены();
КонецФункции



Функция ПолучитьНомерОтчетаБезГашения() Экспорт
	возврат Сам().ИнтерфейсОборудования.ПолучитьНомерОтчетаБезГашения();
КонецФункции

Функция ПолучитьНомерВнесения() Экспорт
	возврат Сам().ИнтерфейсОборудования.ПолучитьНомерВнесения();
КонецФункции

Функция ПолучитьНомерВыплаты() Экспорт
	возврат Сам().ИнтерфейсОборудования.ПолучитьНомерВыплаты();
КонецФункции

//максимальное количество символов в строке чека ККМ
Функция ПолучитьДлинуСтрокиЧека() Экспорт
	возврат Сам().ИнтерфейсОборудования.ПолучитьДлинуСтрокиЧека();
КонецФункции

//возвращает структуру с показателями нумераторов чеков продаж, покупок, возвратов продаж, возвратов покупок,
//внесений, выплаты, номеров смен, номеров X-отчетов
Функция ПрочитатьСчетчики() Экспорт
	возврат Сам().ИнтерфейсОборудования.ПрочитатьСчетчики();
КонецФункции

Функция ПрочитатьДенежныеСчетчики() Экспорт
	возврат Сам().ИнтерфейсОборудования.ПрочитатьДенежныеСчетчики();
КонецФункции

Процедура ПоказатьСчетчики() Экспорт
	Счетчики=Сам().ПрочитатьСчетчики();
	глБраузер(Счетчики);
КонецПроцедуры

Процедура ПоказатьДенежныеСчетчики() Экспорт
	Счетчики=Сам().ПрочитатьДенежныеСчетчики();
	глБраузер(Счетчики);
КонецПроцедуры



Функция АннулироватьЧек() Экспорт
	Сам=Сам();
	Код=Сам.ИнтерфейсОборудования.АннулироватьЧек();
	Если Сам.НетОшибокВДрайвере()=1 Тогда
		Возврат 1;
	КонецЕсли;
	Сам.УстановитьОшибку("Ошибка аннулирования чека.
	|Состояние драйвера:"+Сам.ИнтерфейсОборудования.ПоследняяОшибка());
	возврат 0;
КонецФункции


//открывает денежный ящик
Функция ОткрытьДенежныйЯщик() Экспорт

	Сам=Сам();
	Рез= Сам.ИнтерфейсОборудования.ОткрытьДенежныйЯщик();
	Если Рез=0 Тогда
		Сам.УстановитьОшибку(Сам.ИнтерфейсОборудования.ПоследняяОшибка());
	Иначе
		Сам.СброситьОшибку();
	КонецЕсли;
	Возврат Рез;
КонецФункции

Функция ПродолжитьПечать() Экспорт


	Сам=Сам();
	Рез= Сам.ИнтерфейсОборудования.ПродолжитьПечать();
	Если Рез=0 Тогда
		Сам.УстановитьОшибку(Сам.ИнтерфейсОборудования.ПоследняяОшибка());
	Иначе
		Сам.СброситьОшибку();
	КонецЕсли;
	Возврат Рез;
КонецФункции


Функция ВнешнееСобытие(Издатель,Событие) Экспорт
	Сам=Сам();
	Отработали=0;

	Если Событие.Источник="Команда" Тогда

		Команда=Событие.Описатель.Команда;

		Если Команда="ЗаписатьКонтрольнуюЛенту" Тогда
			КонтрольнаяЛента.ЗаписатьТекст(Событие.Описатель.Данные);
			Отработали=1;
		ИначеЕсли Команда="ЗаписатьЛогНачалоЧека" Тогда
			КонтрольнаяЛента.ЗаписатьПризнакНачалаЧека(Событие.Описатель.Данные);
			Отработали=1;
		ИначеЕсли Команда="ЗаписатьЛогЧекНеЗакрыт" Тогда
			КонтрольнаяЛента.ЗаписатьПризнакКонцаЧека(0);
			Отработали=1;
		ИначеЕсли Команда="ЗаписатьЛогЧекЗакрыт" Тогда
			КонтрольнаяЛента.ЗаписатьПризнакКонцаЧека(1);
			Отработали=1;
		КонецЕсли;
	КонецЕсли;

	Если Отработали = 1 Тогда
		глСобытиеОбработано(Событие);
		Сам.ОбновитьФорму();
		Возврат 0;
	КонецЕсли;
	возврат 1;
КонецФункции


Функция Инит(РежимИнициализации=0) Экспорт
	Рез=_База.Инит(РежимИнициализации);
	КонтрольнаяЛента.Инит(Сам());
	Возврат Рез;
КонецФункции