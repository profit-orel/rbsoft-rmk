//Конфигурация для платформы "1С:Предприятие 7.7" РБ-Софт:Рабочее место кассира.
//Версия 4.57 от 05.06.11 23:24:52
//(c) Баинов Евгений, ООО "РБ-Софт" 2006-2011 г.Улан-Удэ, ул.Балтахинова,17Е,офис 215
//e-mail: bainov@rb-soft.ru  icq: 73718937, телефоны: (3012) 440-357, 278-228
//Сайт проекта: http://code.google.com/p/rbsoft-rmk/
//_______________________________________________________________________________________
//Класс РедакторСтроки
//_______________________________________________________________________________________
Перем _База Экспорт;

Перем Буфер Экспорт; //Редактируемая строка
Перем МаксимальнаяДлинаБуфера Экспорт; //Максимально возможная длина редактируемой строки

Перем Курсор;//число. позиция курсора
Перем Клавиатура; //ссылка на объект-клавиатура
Перем ДопустимыеСимволы;
Перем ДопустимыеСимволыВсе;
Перем ДопустимыеСимволыЧисла;
Перем ДесятичныхЗнаков;//количество разрешенных знаков после запятой
Перем РежимЧисел;
Перем РазрешитьВедущиеНули;

Функция Этот(Конт) 		Возврат Конт; 			КонецФункции
Функция Сам() 			Возврат Этот(Контекст) 	КонецФункции



Процедура Сброс() Экспорт
	Буфер="";
	Курсор=0;
	Сам().ОбновитьФорму();
КонецПроцедуры


//------------------------------------ ------------------------------------
Процедура Конструктор()

	_База = Этот(Контекст).ПолучитьБазовыйКласс("Объект");
	Сам=Сам();
	Клавиатура=глДрайверы.Клавиатура;

	ДопустимыеСимволыЧисла="0123456789.";
	ДопустимыеСимволыВсе="";
	ДопустимыеСимволы=ДопустимыеСимволыЧисла;

	МаксимальнаяДлинаБуфера=17;

	ДесятичныхЗнаков=0;

	РежимЧисел=1;
	РазрешитьВедущиеНули=1;
	Сброс();
КонецПроцедуры // Конструктор


//возвращает режим разрешения ввода нулей в начале строки ввода
//может установить режим разрешения или запрета ведущих нулей
Функция ВедущиеНули(_РазрешитьВедущиеНули=-1) Экспорт
	Если _РазрешитьВедущиеНули=-1 Тогда
		Возврат РазрешитьВедущиеНули;
	КонецЕсли;
	Если _РазрешитьВедущиеНули=0 Тогда
		РазрешитьВедущиеНули=0;
	Иначе
		_РазрешитьВедущиеНули=1;
	КонецЕсли;
	РазрешитьВедущиеНули=_РазрешитьВедущиеНули
КонецФункции

Процедура ЗапретитьВедущиеНули() Экспорт
	РазрешитьВедущиеНули=0;
КонецПроцедуры

Процедура РазрешитьВедущиеНули() Экспорт
	РазрешитьВедущиеНули=1;
КонецПроцедуры


//------------------------------------ ------------------------------------
Процедура Деструктор()

КонецПроцедуры // Деструктор


Функция ДопустимыйСимвол(Символы) Экспорт
	Если ДопустимыеСимволы="" Тогда
		Возврат 1;
	КонецЕсли;

	Если СтрДлина(Символы)=1 Тогда
		Если Найти(ДопустимыеСимволы,Символы)>0 Тогда

			Если СтрДлина(Буфер)>=МаксимальнаяДлинаБуфера Тогда
				возврат 0;
			ИначеЕсли РежимЧисел=1 Тогда
				Если Буфер="0" Тогда
					Если Символы="0" Тогда
						Возврат ВедущиеНули();
					КонецЕсли;
				ИначеЕсли Буфер="" Тогда
					Если Символы="." Тогда
						Возврат 0;
					КонецЕсли;
				КонецЕсли;
			Иначе
				Возврат 1;
			КонецЕсли;
			Возврат 1;
		Иначе
			Возврат 0;
		КонецЕсли;
	ИначеЕсли СтрДлина(Символы)>1 Тогда
		Если СтрДлина(Буфер)+СтрДлина(Символы)>МаксимальнаяДлинаБуфера Тогда
			возврат 0;
		Иначе
			Для Инд=1 По СтрДлина(Символы) Цикл
				Если ДопустимыйСимвол(Сред(Символы,Инд,1))=0 Тогда
					Возврат 0;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Возврат 1;
	КонецЕсли;
	Возврат 0;
КонецФункции

Функция УстановитьБуфер(Значение) Экспорт
	Сам=Сам();

	Если РежимЧисел=1 Тогда
		Если ТипЗначенияСтр(Значение)="Строка" Тогда
			Попытка
				Буфер=Сам.УстановитьБуфер(Число(Значение));
			Исключение
				Буфер=Сам.УстановитьБуфер(0);
			КонецПопытки;
		Иначе
			Буфер=Формат(Значение,"Ч15."+ДесятичныхЗнаков);
		КонецЕсли;
	Иначе
		Если ТипЗначенияСтр(Значение)="Строка" Тогда
			Буфер=Значение;
		Иначе
			Буфер=Строка(Значение);
		КонецЕсли;
	КонецЕсли;

	Сам.ТребуетсяОтображение();
	Возврат 1;
КонецФункции

Функция ОбработатьСимвол(Символ,Событие)

	Если ДопустимыйСимвол(Символ)=1 Тогда
		Если РежимЧисел=1 Тогда
			Если Буфер="0" Тогда
				Если (Символ="0")Или(Символ="00") Тогда
					Если ВедущиеНули()=1 Тогда
						//разрешено вводить ведущие нули
						Буфер=Буфер+Символ;
					КонецЕсли;
				ИначеЕсли (Символ=".") Тогда
					Буфер="0.";
				Иначе

					Если ВедущиеНули()=1 Тогда
						//разрешено вводить ведущие нули
						Буфер=Буфер+Символ;
					Иначе
						Буфер=Символ;
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли Буфер="" Тогда
				Если (Символ=".") Тогда
					Буфер="0.";
				ИначеЕсли (Символ="00") Тогда
					Если ВедущиеНули()=1 Тогда
						//разрешено вводить ведущие нули
						Буфер=Символ;
					Иначе
						Буфер="0";
					КонецЕсли;
				Иначе
					Буфер=Символ;
				КонецЕсли;
			ИначеЕсли Символ="." Тогда
				ПозицияЗапятой=Найти(Буфер,".");
				Если ПозицияЗапятой=0 Тогда
					Буфер=Буфер+Символ;
				КонецЕсли;
			Иначе
				Буфер=Буфер+Символ;
			КонецЕсли;
			
			Если ДесятичныхЗнаков>0 Тогда
				ПозицияЗапятой=Найти(Буфер,".");
				Если ПозицияЗапятой>0 Тогда
		
					КоличествоЗнаковПослеЗапятой=СтрДлина(Буфер)-ПозицияЗапятой;
					Если КоличествоЗнаковПослеЗапятой>ДесятичныхЗнаков Тогда
						Буфер=Лев(Буфер,ПозицияЗапятой+ДесятичныхЗнаков);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			Событие.Обработано=1;
		Иначе
			Если СтрДлина(Символ)=1 Тогда
				СимволБуфер=Символ;
			ИначеЕсли Символ="Пробел" Тогда	
				СимволБуфер=" ";
			Иначе
				Событие.Обработано=1;
			КонецЕсли;
			Буфер=Буфер+СимволБуфер;
		КонецЕсли;

	КонецЕсли;

	
КонецФункции

Функция ОбработкаСобытий(Издатель,Событие) Экспорт
	Сам=Сам();

	Сообщить(Сам.Вид());


	Если Событие.Источник="Клавиатура" Тогда

		Если Событие.Обработано=0 Тогда

			Символ=Клавиатура.ПолучитьСимвол(Событие.Описатель.Данные);

			Если Символ<>"" Тогда
				ОбработатьСимвол(Символ,Событие);
			КонецЕсли;	


			Если Событие.Обработано=0 Тогда
				Команда=глДрайверы.Клавиатура.ПолучитьКомандуРедактораСтроки(Событие.Описатель.Данные);
				
				Если Команда<>"" Тогда
					СобытиеКоманда=глПослатьКоманду(Сам,Команда);
					Возврат СобытиеКоманда.Обработано;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Событие.Источник="Команда" Тогда
		Команда=Событие.Описатель.Команда;



		Если Команда="СбросРедактора" Тогда

			Сам.Сброс();
			Событие.Обработано=1;
		ИначеЕсли Команда="УстановитьЗначениеРедактора" Тогда

			УстановитьБуфер(Событие.Описатель.Данные);
			Событие.Обработано=1;

		ИначеЕсли Команда="EDIT_ЗАБОЙ" Тогда
			Если СтрДлина(Буфер)>0 Тогда
				Буфер=Лев(Буфер,СтрДлина(Буфер)-1);
			КонецЕсли;
			Событие.Обработано=1;

		ИначеЕсли Найти("0/1/2/3/4/5/6/7/8/9/00/.",Команда)>0 Тогда
			Символ=Команда;
			ОбработатьСимвол(Символ,Событие);
		КонецЕсли;
	КонецЕсли;

	Если Событие.Обработано=1 Тогда

		глПослатьКоманду(Сам,"ИзменилсяРедакторСтроки",Сам.Буфер);
		Сам.ОбновитьФорму();
		возврат 0;
	КонецЕсли;
	возврат 1;
КонецФункции

Функция КоличествоДесятичныхЗнаков(_ДесятичныхЗнаков=-1) Экспорт
	Если _ДесятичныхЗнаков=-1 Тогда
		Возврат ДесятичныхЗнаков;
	ИначеЕсли _ДесятичныхЗнаков=0 Тогда
		ДопустимыеСимволы="0123456789";
	Иначе
		ДопустимыеСимволы="0123456789.";
	КонецЕсли;
	ДесятичныхЗнаков=_ДесятичныхЗнаков;;

	Возврат ДесятичныхЗнаков;
КонецФункции



Функция РежимЧисел(_РежимЧисел=-1) Экспорт
	Если _РежимЧисел=-1 Тогда
		Возврат РежимЧисел;
	ИначеЕсли _РежимЧисел=1 Тогда 
		РежимЧисел=1;
		ДопустимыеСимволы=ДопустимыеСимволыЧисла;
	ИначеЕсли _РежимЧисел=0 Тогда 
		РежимЧисел=0;
		ДопустимыеСимволы=ДопустимыеСимволыВсе;
	КонецЕсли;
	
	Возврат РежимЧисел;
КонецФункции


Функция КакЧисло() Экспорт
	Попытка
		возврат Число(СокрЛП(Буфер));
	Исключение
	КонецПопытки;
	возврат 0;
КонецФункции

Функция КакСтрока() Экспорт
	возврат СокрЛП(Буфер);
КонецФункции

Функция ПредставлениеСтроки() Экспорт
	Возврат Строка(Буфер);
КонецФункции
