//Конфигурация для платформы "1С:Предприятие 7.7" РБ-Софт:Рабочее место кассира.
//Версия 4.57 от 05.06.11 23:24:52
//(c) Баинов Евгений, ООО "РБ-Софт" 2006-2011 г.Улан-Удэ, ул.Балтахинова,17Е,офис 215
//e-mail: bainov@rb-soft.ru  icq: 73718937, телефоны: (3012) 440-357, 278-228
//Сайт проекта: http://code.google.com/p/rbsoft-rmk/
//_______________________________________________________________________________________
//Экранная форма класса Товар
//Имя формы : ФормаСписка
//_______________________________________________________________________________________
Перем СтруктураПараметров;
Перем Объект;

Перем ТабличноеПоле;
Перем ФильтрВключен;
Перем РасширениеФормы;
Перем СтараяСтрока;
Перем РежимОтбора;
Перем РежимВыбора;

Перем РедакторСтроки;


Процедура ОтобразитьФильтр()


	Попытка
		Если ФильтрВключен=1 Тогда
			Активизировать("СтрокаПоиска");
		Иначе
			Активизировать("кнТабличноеПоле");
		КонецЕсли;

	Исключение
	КонецПопытки;

	ИмяПоля=ТабличноеПоле.ПолеОтбора();
	Если ИмяПоля="id" Тогда
		Форма.НадписьСтрокаПоиска.Заголовок("Поиск по коду:");
	ИначеЕсли ИмяПоля="article" Тогда
		Форма.НадписьСтрокаПоиска.Заголовок("Поиск по артикулу:");
	ИначеЕсли ИмяПоля="price" Тогда
		Форма.НадписьСтрокаПоиска.Заголовок("Поиск по цене:");
	ИначеЕсли ИмяПоля="name" Тогда
		Форма.НадписьСтрокаПоиска.Заголовок("Поиск по наименованию:");
	Иначе
		Форма.НадписьСтрокаПоиска.Заголовок("Поиск по "+ИмяПоля+":");
	КонецЕсли;


	ТабличноеПоле.ОбновитьСтроки();
	Форма.Обновить();
КонецПроцедуры


Процедура ПослеОткрытия()



	РежимВыбора=0;
	ВыборСделан=0;

	Если ПустоеЗначение(Форма.Параметр)=1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	СтруктураПараметров=Форма.Параметр;
	Объект=СтруктураПараметров.Объект;

	Попытка
		Значение=СтруктураПараметров.РежимВыбора;
		Если ПустоеЗначение(Значение)=0 Тогда
			РежимВыбора=Значение;
		КонецЕсли;
	Исключение
	КонецПопытки;


	РедакторСтроки=СоздатьОбъект("РедакторСтроки");

	Попытка

		ТабличноеПоле=СоздатьОбъект("ТабличноеПолеСпискаТоваров");
		ТабличноеПоле.Инит(Контекст,"кнТабличноеПоле",РежимВыбора);

		КодГруппыПоУмолчанию=глНастройки.Получить("SETTINGS/DEFAULTGOODSGROUP");
		Если ПустоеЗначение(КодГруппыПоУмолчанию)=0 Тогда
		    ТабличноеПоле.УстановитьРодителя(КодГруппыПоУмолчанию,"");
		Иначе
			ТабличноеПоле.УстановитьРодителя("","");
		КонецЕсли;  //



		Если РежимВыбора=0 Тогда
			ТабличноеПоле.ВводНовыхЭлементов(1);
		КонецЕсли;


		КодРодителя="";
		Активизировать("кнТабличноеПоле");
	Исключение
		глЗаписатьСобытие("","Товары_ФормаСписка:ПослеОткрытия()",ОписаниеОшибки());
	КонецПопытки;

	ОтобразитьФильтр();
КонецПроцедуры	// ПослеОткрытия

Процедура ВключитьФильтр()
	Если СтрДлина(СокрЛП(СтрокаПоиска))>0 Тогда
		Если СтараяСтрока<>СокрЛП(СтрокаПоиска) Тогда
			СтараяСтрока=СокрЛП(СтрокаПоиска);
			ТабличноеПоле.УстановитьФильтр(СокрЛП(СтрокаПоиска),300);
		КонецЕсли;
	Иначе

		Если СокрЛП(СтрокаПоиска)="" Тогда
			СтараяСтрока="";
			ТабличноеПоле.УстановитьФильтр("");
			ОтобразитьФильтр();
		КонецЕсли;
	КонецЕсли;
	ТабличноеПоле.ОбновитьСтроки();
	Форма.Обновить();
КонецПроцедуры



Процедура Фильтр(ИмяПоля)
	ТабличноеПоле.ПолеОтбора(ИмяПоля);


    Если ФильтрВключен=1 Тогда
		Если Форма.АктивныйЭлемент()="кнТабличноеПоле" Тогда
			Активизировать("СтрокаПоиска");
			Сервис = СоздатьОбъект("Сервис");
 			Сервис.SendKeys("{RIGHT}");
		Иначе
			ФильтрВключен=0;
			ТабличноеПоле.УстановитьФильтр("");
		КонецЕсли;

	Иначе
		//СтрокаПоиска="";
		ФильтрВключен=1;
	КонецЕсли;

	ВключитьФильтр();

КонецПроцедуры

Процедура Справка()
	Предупреждение("В поле поиска набираем слова через пробел. Поиск производится на одновременное вхождение слов в поле поиска.
	|Пример поиска по цене товара: первое слово в строке поиска считается ценой товара, остальные - это слова поиска по наименованию.
	|1)Если в строке поиска мы наберем: 5200 телефон NOKIA
	|  Будет произведен поиск по всем телефонам NOKIA с ценой 5200 рублей.
	|2)Если в строке поиска мы наберем: 5200-10000 телефон NOKIA
	|                                   или
	|                                   10000-5200 телефон NOKIA
	|  Будет произведен поиск по всем телефонам NOKIA с ценой от 5200 до 10000 рублей.
	|");
КонецПроцедуры




Процедура ПриНажатииКнопкиКлавиатуры(пКодКлавиши, Alt, Shift, Ctrl, Символ, ФСО)
	Перем лСимвол,лКоманда,лКодКлавиши;
	Если Форма.АктивныйЭлемент()="СтрокаПоиска" Тогда
		возврат;
	КонецЕсли;

	лКодКлавиши=глДрайверы.Клавиатура.ПолучитьКодКлавиши(пКодКлавиши,Alt,Shift,Ctrl);

	Если лКодКлавиши = глДрайверы.Клавиатура.КнопкаПодбораПоЦене Тогда
		Фильтр("price");
		ФСО=0;
		возврат;
	ИначеЕсли лКодКлавиши = глДрайверы.Клавиатура.КнопкаПодбораПоКоду Тогда
		Фильтр("id");
		ФСО=0;
		возврат;
	ИначеЕсли лКодКлавиши = глДрайверы.Клавиатура.КнопкаПодбораТовара Тогда
		Фильтр("name");
		ФСО=0;
		возврат;
	ИначеЕсли лКодКлавиши = глДрайверы.Клавиатура.КнопкаПодбораПоАртикулу Тогда
		Фильтр("article");
		ФСО=0;
		возврат;
	ИначеЕсли лКодКлавиши = глДрайверы.Клавиатура.КнопкаАктивацииСтрокиПоиска Тогда
		Активизировать("СтрокаПоиска",1);
		ФСО=0;
		возврат;
	Иначе


	КонецЕсли;

	Если ФильтрВключен=0 Тогда
	    Если ТабличноеПоле.ИерархическийСписок() = 1 Тогда
			Если лКодКлавиши = глДрайверы.Клавиатура.КнопкаВниз Тогда
				Если Ctrl = 1 Тогда
					ТабличноеПоле.ПерейтиВГруппу();
				КонецЕсли;
			ИначеЕсли (лКодКлавиши= глДрайверы.Клавиатура.КнопкаВправо) Тогда
				ФСО = 0;
				ТабличноеПоле.ПерейтиВГруппу();

			ИначеЕсли (лКодКлавиши = глДрайверы.Клавиатура.КнопкаВверх) Тогда
				Если Ctrl = 1 Тогда
					ФСО = 0;
					ТабличноеПоле.Вверх();
				КонецЕсли;
			ИначеЕсли (лКодКлавиши = глДрайверы.Клавиатура.КнопкаЗабой) Тогда
				ФСО = 0;
				ТабличноеПоле.Вверх();
			ИначеЕсли (лКодКлавиши = глДрайверы.Клавиатура.КнопкаВлево) Тогда
				ФСО = 0;
				ТабличноеПоле.Вверх();

			КонецЕсли;
		КонецЕсли;
	КонецЕсли;



КонецПроцедуры



Процедура кнТабличноеПоле_ВыполнитьВыбор(ВыбранныйОбъект,ДопДанные="")

	Если СтруктураПараметров.РежимВыбора=1 Тогда
		СтруктураПараметров.Результат=1;
		СтруктураПараметров.Данные=ВыбранныйОбъект;
		Форма.Параметр=СтруктураПараметров;
		Форма.Закрыть();
	ИначеЕсли СтруктураПараметров.РежимВыбора=0 Тогда
		ВыбранныйОбъект.ОткрытьФормуОбъекта();
	КонецЕсли;
КонецПроцедуры


ФильтрВключен=0;
СтараяСтрока="";
РежимОтбора="Наименование";