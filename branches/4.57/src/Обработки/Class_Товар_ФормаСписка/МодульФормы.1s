//Конфигурация для платформы "1С:Предприятие 7.7" РБ-Софт:Рабочее место кассира.
//Версия 4.57 от 05.06.11 23:24:52
//(c) Баинов Евгений, ООО "РБ-Софт" 2006-2011 г.Улан-Удэ, ул.Балтахинова,17Е,офис 215
//e-mail: bainov@rb-soft.ru  icq: 73718937, телефоны: (3012) 440-357, 278-228
//Сайт проекта: http://code.google.com/p/rbsoft-rmk/
//_______________________________________________________________________________________
//Экранная форма класса Товар
//Имя формы : ФормаСписка
//_______________________________________________________________________________________
Перем СтруктураПараметров;
Перем Объект;

Перем ТабличноеПоле;

Перем РасширениеФормы;

Перем РежимОтбора;
Перем РежимВыбора;

Перем РедакторСтроки;


Процедура ОтобразитьФильтр()


	Активизировать("кнТабличноеПоле");

	ИмяПоля=ТабличноеПоле.ПолеОтбора();
	Если ИмяПоля="id" Тогда
		Форма.НадписьСтрокаПоиска.Заголовок("Поиск по коду:");
	ИначеЕсли ИмяПоля="article" Тогда
		Форма.НадписьСтрокаПоиска.Заголовок("Поиск по артикулу:");
	ИначеЕсли ИмяПоля="price" Тогда
		Форма.НадписьСтрокаПоиска.Заголовок("Поиск по цене:");
	ИначеЕсли ИмяПоля="name" Тогда
		Форма.НадписьСтрокаПоиска.Заголовок("Поиск по наименованию:");
	Иначе
		Форма.НадписьСтрокаПоиска.Заголовок("Поиск по "+ИмяПоля+":");
	КонецЕсли;


	ТабличноеПоле.ОбновитьСтроки();
	Форма.Обновить();
КонецПроцедуры

Процедура ОтобразитьРежим()
	Форма.Обновить();
КонецПроцедуры


Функция СостояниеРедактора()
	Попытка
		возврат РедакторСтроки.ПредставлениеСтроки();
	Исключение
		Возврат "";
	КонецПопытки;
	
КонецФункции

Процедура ПослеСозданияФормы()



	РежимВыбора=0;
	ВыборСделан=0;

	Если ПустоеЗначение(Форма.Параметр)=1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	СтруктураПараметров=Форма.Параметр;
	Объект=СтруктураПараметров.Объект;

	Попытка
		Значение=СтруктураПараметров.РежимВыбора;
		Если ПустоеЗначение(Значение)=0 Тогда
			РежимВыбора=Значение;
		КонецЕсли;
	Исключение
	КонецПопытки;


	РедакторСтроки=СоздатьОбъект("РедакторСтроки");
	РедакторСтроки.УстановитьКонтекст(Контекст);
	РедакторСтроки.РежимЧисел(0);
	РедакторСтроки.МаксимальнаяДлинаБуфера=200;
	

	Попытка

		ТабличноеПоле=СоздатьОбъект("ТабличноеПолеСпискаТоваров");
		ТабличноеПоле.Инит(Контекст,"кнТабличноеПоле",РежимВыбора);

		КодГруппыПоУмолчанию=глНастройки.Получить("SETTINGS/DEFAULTGOODSGROUP");
		Если ПустоеЗначение(КодГруппыПоУмолчанию)=0 Тогда
		    ТабличноеПоле.УстановитьРодителя(КодГруппыПоУмолчанию,"");
		Иначе
			ТабличноеПоле.УстановитьРодителя("","");
		КонецЕсли;  //



		Если РежимВыбора=0 Тогда
			ТабличноеПоле.ВводНовыхЭлементов(1);
		КонецЕсли;


		КодРодителя="";
		Активизировать("кнТабличноеПоле");
	Исключение
		глЗаписатьСобытие("","Товары_ФормаСписка:ПослеОткрытия()",ОписаниеОшибки());
	КонецПопытки;

	ОтобразитьФильтр();
	
	глПодписатьсяНаСобытия(РедакторСтроки);
КонецПроцедуры	// ПослеОткрытия


Процедура ВыключитьФильтр()
	РедакторСтроки.Сброс();
	ТабличноеПоле.УстановитьФильтр("");
	ОтобразитьФильтр();
КонецПроцедуры

Процедура Фильтр(ИмяПоля)
	ТабличноеПоле.ПолеОтбора(ИмяПоля);
   
	ТабличноеПоле.УстановитьФильтр(РедакторСтроки.КакСтрока(),300);
	ОтобразитьФильтр();
КонецПроцедуры

Процедура Справка()
	Предупреждение("В поле поиска набираем слова через пробел. Поиск производится на одновременное вхождение слов в поле поиска.
	|Запуск поиска по выбранному полю производится нажатием соответствующей кнопки:
	|""Фильтр по коду"", ""Фильтр по наименованию"", ""Фильтр по артикулу"", ""Фильтр по цене""
	|");
КонецПроцедуры




Процедура ПриНажатииКнопкиКлавиатуры(пКодКлавиши, Alt, Shift, Ctrl, Символ, ФСО)
	Перем лСимвол,лКоманда,лКодКлавиши;
	
	лКодКлавиши=глДрайверы.Клавиатура.ПолучитьКодКлавиши(пКодКлавиши,Alt,Shift,Ctrl);

	Если лКодКлавиши = глДрайверы.Клавиатура.КнопкаПодбораПоЦене Тогда
		Фильтр("price");
		ФСО=0;
		возврат;
	ИначеЕсли лКодКлавиши = глДрайверы.Клавиатура.КнопкаПодбораПоКоду Тогда
		Фильтр("id");
		ФСО=0;
		возврат;
	ИначеЕсли лКодКлавиши = глДрайверы.Клавиатура.КнопкаПодбораТовара Тогда
		Фильтр("name");
		ФСО=0;
		возврат;
	ИначеЕсли лКодКлавиши = глДрайверы.Клавиатура.КнопкаПодбораПоАртикулу Тогда
		Фильтр("article");
		ФСО=0;
		возврат;
	ИначеЕсли лКодКлавиши = глДрайверы.Клавиатура.КнопкаАктивацииСтрокиПоиска Тогда
		Активизировать("СтрокаПоиска",1);
		ФСО=0;
		возврат;
	Иначе
		

	КонецЕсли;

	Если Форма.АктивныйЭлемент()="кнТабличноеПоле" Тогда
		
	    Если ТабличноеПоле.ИерархическийСписок() = 1 Тогда
			Если лКодКлавиши = глДрайверы.Клавиатура.КнопкаВниз Тогда
				Если Ctrl = 1 Тогда
					ТабличноеПоле.ПерейтиВГруппу();
					Возврат;
				КонецЕсли;
			ИначеЕсли (лКодКлавиши= глДрайверы.Клавиатура.КнопкаВправо) Тогда
				ФСО = 0;
				ТабличноеПоле.ПерейтиВГруппу();
				Возврат;
			ИначеЕсли (лКодКлавиши = глДрайверы.Клавиатура.КнопкаВверх) Тогда
				Если Ctrl = 1 Тогда
					ФСО = 0;
					ТабличноеПоле.Вверх();
					Возврат;
				КонецЕсли;
			ИначеЕсли (лКодКлавиши = глДрайверы.Клавиатура.КнопкаЗабой) Тогда
				ФСО = 0;
				ТабличноеПоле.Вверх();
				Возврат;
			ИначеЕсли (лКодКлавиши = глДрайверы.Клавиатура.КнопкаВлево) Тогда
				ФСО = 0;
				ТабличноеПоле.Вверх();
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;


	глПриНажатииКнопкиКлавиатуры(Контекст,пКодКлавиши,Alt,Shift,Ctrl);

КонецПроцедуры



Процедура кнТабличноеПоле_ВыполнитьВыбор(ВыбранныйОбъект,ДопДанные="")

	Если СтруктураПараметров.РежимВыбора=1 Тогда
		СтруктураПараметров.Результат=1;
		СтруктураПараметров.Данные=ВыбранныйОбъект;
		Форма.Параметр=СтруктураПараметров;
		Форма.Закрыть();
	ИначеЕсли СтруктураПараметров.РежимВыбора=0 Тогда
		ВыбранныйОбъект.ОткрытьФормуОбъекта();
	КонецЕсли;
КонецПроцедуры


Процедура ПриЗакрытии()
	глОтписатьсяОтСобытий(РедакторСтроки);
	РедакторСтроки=0;
	ТабличноеПоле=0;
КонецПроцедуры


РежимОтбора="Наименование";