//Конфигурация для платформы "1С:Предприятие 7.7" РБ-Софт:Рабочее место кассира.
//Версия 4.43 от 19.07.10 20:54:20
//Назначение: Автоматизация POS-узлов в магазинах (Point of sale).
//(c) Баинов Евгений, ООО "РБ-Софт" 2006, г.Улан-Удэ, проспект 50-летия Октября,21А,офис 110 
//e-mail: bainov@rb-soft.ru  icq: 73718937, телефоны: (3012) 44-03-57, 44-76-42, 44-86-58
//---------------------------------------------------------------------------------------
//Данная программа является свободным программным обеспечением.
//Вы вправе распространять ее и/или модифицировать в соответствии с условиями версии 2 
//либо по вашему выбору с условиями более поздней версии Стандартной Общественной Лицензии GNU,
//опубликованной Free Software Foundation. 
//Мы распространяем данную программу в надежде на то, что она будет вам полезной, 
//однако НЕ ПРЕДОСТАВЛЯЕМ НА НЕЕ НИКАКИХ ГАРАНТИЙ, 
//в том числе ГАРАНТИИ ТОВАРНОГО СОСТОЯНИЯ ПРИ ПРОДАЖЕ
//и ПРИГОДНОСТИ ДЛЯ ИСПОЛЬЗОВАНИЯ В КОНКРЕТНЫХ ЦЕЛЯХ.
//Для получения более подробной информации ознакомьтесь со Стандартной Общественной Лицензией GNU.
//Вместе с данной программой вы должны были получить экземпляр Стандартной Общественной Лицензии GNU.
//Если вы его не получили, сообщите об этом в Free Software Foundation, Inc.,
//59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
//_______________________________________________________________________________________
//Имя модуля:
// <Вставить имя модуля>
//_______________________________________________________________________________________
//Разработчик: Баинов Евгений, e-mail: bainov@rb-soft.ru , icq: 73718937
//_______________________________________________________________________________________
//Назначение модуля:
// <Вставить описание модуля>
//
//_______________________________________________________________________________________
Перем КолонкаВыбора;
Перем СпособВыбора;
Перем Параметры;

//_____________________________________________________________________________
Процедура РаскраситьТЗ(Номер)

	флагВыбора=ТабВыб.ПолучитьЗначение(Номер,"ФлагВыбора");
	Если флагВыбора=1 Тогда
		ТабВыб.УстановитьЗначение(Номер,"Раскраска",глОбщиеФункции.ЦветТаблицы(глОбщиеФункции.ПолучитьЦвет(255,255,255),,ТабВыб.КоличествоКолонок()));
	ИначеЕсли флагВыбора=2	Тогда
		ТабВыб.УстановитьЗначение(Номер,"Раскраска",глОбщиеФункции.ЦветТаблицы(глОбщиеФункции.ПолучитьЦвет(200,255,200),,ТабВыб.КоличествоКолонок()));

	КонецЕсли;  //

КонецПроцедуры //РаскраситьТЗ

//	Описание: Устанавливает, снимает или инвертирует отметки выбора строк
Процедура Установить(Режим="")
	Если Режим="Все" Тогда
		ТабВыб.Заполнить(2,,,"ФлагВыбора");
	ИначеЕсли Режим="Сброс" Тогда
		ТабВыб.Заполнить(1,,,"ФлагВыбора");
	Иначе
		ТабВыб.ВыбратьСтроки();
		Пока ТабВыб.ПолучитьСтроку()=1 Цикл
			ТабВыб.ФлагВыбора=?(ТабВыб.ФлагВыбора=1,2,1);
			РаскраситьТЗ(ТабВыб.НомерСтроки);
		КонецЦикла;
		возврат;
	КонецЕсли;  //
	ТабВыб.ВыбратьСтроки();
	Пока ТабВыб.ПолучитьСтроку()=1 Цикл
		РаскраситьТЗ(ТабВыб.НомерСтроки);
	КонецЦикла;

КонецПроцедуры  //  Установить



Процедура ПриОткрытии()
	Перем ТекСтр,ТекЭлем;

	Если ТипЗначенияСтр(Форма.Параметр)<>"СписокЗначений" Тогда
		СтатусВозврата(0);
		Предупреждение("Неверные параметры в обработке ВыборИзТаблицы");
		возврат;
	КонецЕсли;
	Параметры=Форма.Параметр;

	ТабВыб.Загрузить(Параметры.Получить("Таблица"));
	Форма.Заголовок("Выбор значения:");

	ТекЭлем=Параметры.Получить("ТекущийЭлемент");
	КолонкаВыбора=Параметры.Получить("Колонка");
	Подсказка=Параметры.Получить("Подсказка");
	СпособВыбора=Параметры.Получить("СпособВыбора");
	Цвета=Параметры.Получить("Цвета");
	СкрытьКолонки=Параметры.Получить("СкрытьКолонки");

	Если ПустоеЗначение(Подсказка)=0 Тогда
		Форма.Заголовок(Строка(Подсказка));
	КонецЕсли;


	Если СпособВыбора=1 Тогда
		ТабВыб.ВставитьКолонку("ФлагВыбора",1,"Число",1,0,"",5);
		ТабВыб.Заполнить(1,,,"ФлагВыбора");
		ТабВыб.ВыводитьПиктограммы(1,1);

		ТабВыб.ВставитьКолонку("Раскраска",1);
		ТабВыб.ВидимостьКолонки("Раскраска",0);
		Форма.ИспользоватьСлой("Основной,Кнопки",2);
	Иначе
		Форма.ИспользоватьСлой("Основной",2);

	КонецЕсли;

	Если ПустоеЗначение(СкрытьКолонки)=0 Тогда
		ТабВыб.ВидимостьКолонки(СкрытьКолонки,0);
	КонецЕсли;



	Если КолонкаВыбора<>"" Тогда


		Нашел=0;
		Для инд=1 По ТабВыб.КоличествоКолонок() Цикл
			Имя=ТабВыб.ПолучитьПараметрыКолонки(Инд);
			Если Имя=КолонкаВыбора Тогда
				Нашел=1;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Нашел=0 Тогда
			СтатусВозврата(0);
			Предупреждение("В таблице выбора нет колонки с именем '"+КолонкаВыбора+"'");
			возврат;
		КонецЕсли;
	Иначе
		//возвращаем номер строки таблицы значений
		//или список номеров
		КолонкаВыбора="НомерСтроки";

	КонецЕсли;  //

	Попытка
		Если (ПустоеЗначение(текЭлем)=0)И(ПустоеЗначение(КолонкаВыбора)=0) Тогда

			Если (ТипЗначенияСтр(текЭлем)="СписокЗначений")И(СпособВыбора=1) Тогда
				//установим галочки, если множественный выбор
				Для Инд=1 По текЭлем.РазмерСписка() Цикл
					ТекСтр=0;
					Стр="";
					ЭлемПоиска=текЭлем.ПолучитьЗначение(Инд,Стр);
					Если ТабВыб.НайтиЗначение(ЭлемПоиска,ТекСтр,КолонкаВыбора)=1 Тогда
						ТабВыб.ПолучитьСтрокуПоНомеру(ТекСтр);
						ТабВыб.ФлагВыбора=2;
					КонецЕсли;
					РаскраситьТЗ(ТекСтр);
				КонецЦикла;
			КонецЕсли;
		Иначе
			ТекСтр=0;
			Если ТабВыб.НайтиЗначение(текЭлем,ТекСтр,КолонкаВыбора)=1 Тогда
				ТабВыб.ТекущаяСтрока(ТекСтр);
			КонецЕсли;

		КонецЕсли;



	Исключение
		СтатусВозврата(0);
		Предупреждение(ОписаниеОшибки());
		возврат;
	КонецПопытки;

	Если ТабВыб.КоличествоСтрок()=0 Тогда
		Форма.кнОК.Доступность(0);
	КонецЕсли;
КонецПроцедуры
//*******************************************
Процедура Выбрать(Режим=0)
	Попытка
		ТабВыб.ПолучитьСтрокуПоНомеру(ТабВыб.ТекущаяСтрока());

		Если СпособВыбора=1 Тогда

			Если Режим=1 Тогда
				//нажат ентер на таблице
				ТабВыб.ФлагВыбора=?(ТабВыб.ФлагВыбора=1,2,1);
				РаскраситьТЗ(ТабВыб.НомерСтроки);
				возврат;
			Иначе
				//нажата кнопка "выбрать"
				Выбор=СоздатьОбъект("СписокЗначений");
				ТабВыб.ВыбратьСтроки();
				Пока ТабВыб.ПолучитьСтроку()=1 Цикл
					Если ТабВыб.ФлагВыбора=2 Тогда
						Если КолонкаВыбора="НомерСтроки" Тогда
						    Выбор.ДобавитьЗначение(ТабВыб.НомерСтроки);
						Иначе
							Выбор.ДобавитьЗначение(ТабВыб.ПолучитьЗначение(ТабВыб.НомерСтроки,КолонкаВыбора));
						КонецЕсли;  //


					КонецЕсли;
				КонецЦикла;
				Параметры.Установить("Выбор",Выбор);
			КонецЕсли;
		Иначе
			//режим выбора одного элемента
			Если КолонкаВыбора="НомерСтроки" Тогда
				Параметры.Установить("Выбор",ТабВыб.НомерСтроки);
			Иначе
				Параметры.Установить("Выбор",ТабВыб.ПолучитьЗначение(ТабВыб.НомерСтроки,КолонкаВыбора));
			КонецЕсли;  //

		КонецЕсли;

		Параметры.Установить("СтатусВозврата",1);

	Исключение
		Параметры.Установить("СтатусВозврата",0);
	КонецПопытки;
	Форма.Параметр=Параметры;
	Форма.Закрыть();
КонецПроцедуры

