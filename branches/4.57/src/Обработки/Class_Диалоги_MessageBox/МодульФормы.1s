//Конфигурация для платформы "1С:Предприятие 7.7" РБ-Софт:Рабочее место кассира.
//Версия 4.43 от 19.07.10 20:54:19
//Назначение: Автоматизация POS-узлов в магазинах (Point of sale).
//(c) Баинов Евгений, ООО "РБ-Софт" 2006, г.Улан-Удэ, проспект 50-летия Октября,21А,офис 110 
//e-mail: bainov@rb-soft.ru  icq: 73718937, телефоны: (3012) 44-03-57, 44-76-42, 44-86-58
//---------------------------------------------------------------------------------------
//Данная программа является свободным программным обеспечением.
//Вы вправе распространять ее и/или модифицировать в соответствии с условиями версии 2 
//либо по вашему выбору с условиями более поздней версии Стандартной Общественной Лицензии GNU,
//опубликованной Free Software Foundation. 
//Мы распространяем данную программу в надежде на то, что она будет вам полезной, 
//однако НЕ ПРЕДОСТАВЛЯЕМ НА НЕЕ НИКАКИХ ГАРАНТИЙ, 
//в том числе ГАРАНТИИ ТОВАРНОГО СОСТОЯНИЯ ПРИ ПРОДАЖЕ
//и ПРИГОДНОСТИ ДЛЯ ИСПОЛЬЗОВАНИЯ В КОНКРЕТНЫХ ЦЕЛЯХ.
//Для получения более подробной информации ознакомьтесь со Стандартной Общественной Лицензией GNU.
//Вместе с данной программой вы должны были получить экземпляр Стандартной Общественной Лицензии GNU.
//Если вы его не получили, сообщите об этом в Free Software Foundation, Inc.,
//59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
//_______________________________________________________________________________________
//Имя модуля:
// <Вставить имя модуля>
//_______________________________________________________________________________________
//Разработчик: Баинов Евгений, e-mail: bainov@rb-soft.ru , icq: 73718937
//_______________________________________________________________________________________
//Назначение модуля:
// <Вставить описание модуля>
//
//_______________________________________________________________________________________
Перем Объект;
Перем СтруктураПараметров;

Перем СтароеВремя;

Перем РасширениеФормыТаймер;
Перем ЗначениеТаймера;
Перем ПоКнопке;

Перем СписокКнопок;

Перем ПолеИнфо;


Процедура ВыполнитьВыбор(Кнопка)
	
	Команда=СписокКнопок.Получить(Кнопка);
	
	СтруктураПараметров.Результат=Команда;
	
	ПоКнопке=1;
	
	Форма.Закрыть();
КонецПроцедуры


Процедура ОбновитьТаймер()
	Если ЗначениеТаймера=0 Тогда
		СтруктураПараметров.Результат="Таймаут";
		Форма.Закрыть();
		Возврат;
	КонецЕсли;
	Кнопка=Объект.ПолучитьОбъектПоИмени("тктТаймер");
	Кнопка.УстановитьТекст(Строка(ЗначениеТаймера));
	ЗначениеТаймера=ЗначениеТаймера-1;
	
КонецПроцедуры

//======================================================================
Процедура ПослеСозданияФормы() 
	Если ПустоеЗначение(Форма.Параметр)=1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	СтруктураПараметров=Форма.Параметр;
	Объект=СтруктураПараметров.Объект;
	
	Объект.УстановитьКонтекст(Контекст);
	
	
	
	Режим=СокрЛП(СтруктураПараметров.Данные.Режим);
	
	СписокКнопок=СоздатьОбъект("СписокЗначений");
	
	Если ТипЗначенияСтр(Режим)="Число" Тогда
	  //0 или 'ОК' ('ОК') - кнопка OK;
	  //1 или 'ОК+Отмена' ('OK+Cancel') - кнопки OK и Отмена;
	  //2 или 'Стоп+Повтор+Пропустить' ('Abort+Retry+Ignore') - кнопки Стоп, Повтор, Пропустить;
	  //3 или 'Да+Нет+Отмена' ('Yes+No+Cancel') - кнопки Да, Нет, Отмена;
	  //4 или 'Да+Нет' ('Yes+No') - кнопки Да, Нет;
	  //5 или 'Повтор+Отмена' ('Retry+Cancel') - кнопки Повтор, Отмена;
	  Если Режим=0 Тогда
	  	Режим="ОК";
	  ИначеЕсли Режим=1 Тогда	
	  	Режим="ОК+Отмена";
	  ИначеЕсли Режим=2 Тогда	
	  	Режим="Стоп+Повтор+Пропустить";
	  ИначеЕсли Режим=3 Тогда	
	  	Режим="Да+Нет+Отмена";
	  ИначеЕсли Режим=4 Тогда	
	  	Режим="Да+Нет";
	  ИначеЕсли Режим=5 Тогда	
	  	Режим="Повтор+Отмена";
	  Иначе
	  	Предупреждение("Неверное значение параметра при использовании функции Вопрос.
	  	|Режим="+Режим);
		СтатусВозврата(0);
		Возврат;
	  КонецЕсли;
		
	КонецЕсли;
	
	
		
	КоличествоПараметров=СтрЧислоВхождений(Режим,"+")+1;
	Стр=СтрЗаменить(Режим,"+",РазделительСтрок);
	Если КоличествоПараметров=1 Тогда
		СписокКнопок.ДобавитьЗначение(СокрЛП(СтрПолучитьСтроку(Стр,1)),"ОднаКнопка");
	ИначеЕсли КоличествоПараметров=2 Тогда	
		СписокКнопок.ДобавитьЗначение(СокрЛП(СтрПолучитьСтроку(Стр,1)),"Левая");
		СписокКнопок.ДобавитьЗначение(СокрЛП(СтрПолучитьСтроку(Стр,2)),"Правая");
	ИначеЕсли КоличествоПараметров=3 Тогда	
		СписокКнопок.ДобавитьЗначение(СокрЛП(СтрПолучитьСтроку(Стр,1)),"Первая");
		СписокКнопок.ДобавитьЗначение(СокрЛП(СтрПолучитьСтроку(Стр,2)),"Вторая");
		СписокКнопок.ДобавитьЗначение(СокрЛП(СтрПолучитьСтроку(Стр,3)),"Третья");
	Иначе
		Предупреждение("Неверное количество параметров при использовании функции Вопрос.
	  	|Режим="+Режим);
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
		
	
	
	
	Слой="";
	Если СписокКнопок.РазмерСписка()=1 Тогда
		
		Команда=СписокКнопок.ПолучитьЗначение(1);
		Слой="ОднаКнопка";
		Кнопка=Объект.ПолучитьОбъектПоИмени("кнОднаКнопка");
		Кнопка.УстановитьТекст(Команда);
			
		
	ИначеЕсли СписокКнопок.РазмерСписка()=2 Тогда	
		Команда=СписокКнопок.ПолучитьЗначение(1);
		Кнопка=Объект.ПолучитьОбъектПоИмени("кнЛевая");
		Кнопка.УстановитьТекст(Команда);
		
		Команда=СписокКнопок.ПолучитьЗначение(2);
		Кнопка=Объект.ПолучитьОбъектПоИмени("кнПравая");
		Кнопка.УстановитьТекст(Команда);
		
		Слой="ДвеКнопки";
	ИначеЕсли СписокКнопок.РазмерСписка()=3 Тогда		
		Команда=СписокКнопок.ПолучитьЗначение(1);
		
		Кнопка=Объект.ПолучитьОбъектПоИмени("кнПервая");
		Кнопка.УстановитьТекст(Команда);
		
		
		Команда=СписокКнопок.ПолучитьЗначение(2);
		Кнопка=Объект.ПолучитьОбъектПоИмени("кнВторая");
		Кнопка.УстановитьТекст(Команда);
		
		Команда=СписокКнопок.ПолучитьЗначение(3);
		Кнопка=Объект.ПолучитьОбъектПоИмени("кнТретья");
		Кнопка.УстановитьТекст(Команда);
		
		Слой="ТриКнопки";
	КонецЕсли;
	
	НомерКнопкиПоУмолчанию=1;
	Если ПустоеЗначение(СтруктураПараметров.Данные.КнопкаПоУмолчанию)=0 Тогда
		Поз=СписокКнопок.НайтиЗначение(СтруктураПараметров.Данные.КнопкаПоУмолчанию);
		Если Поз<>0 Тогда
			НомерКнопкиПоУмолчанию=Поз;
		Иначе
			Предупреждение("Назначена кнопка по умолчанию """+СтруктураПараметров.Данные.КнопкаПоУмолчанию+""", 
			|но такой кнопки не обнаружено среди параметров вызова функции глВопрос()
			|Режим="+Режим);
		КонецЕсли;
	КонецЕсли;
	
	

		
	

	
	
	
	ТекстСообщения=СтруктураПараметров.Данные.Текст;
	
	СлойИнфо="Текст";
	Если (СтрДлина(ТекстСообщения)>100)Или(СтрКоличествоСтрок(ТекстСообщения)>5) Тогда
		СлойИнфо="Многострочный";
	КонецЕсли;	
	
	
	ЗначениеТаймера=СтруктураПараметров.Данные.Таймаут;
	
	КнопкаТаймер=Объект.ПолучитьОбъектПоИмени("тктТаймер");
	Если ЗначениеТаймера<>0 Тогда
		
		РасширениеФормыТаймер = СоздатьОбъект("РасширениеФормы");
		РасширениеФормыТаймер.ОбработкаОжидания("ОбновитьТаймер",1000);
		ОбновитьТаймер();
		КнопкаТаймер.Прозрачность(100);
	Иначе
		КнопкаТаймер.Прозрачность(255);
	КонецЕсли;
	
	
		
	Если ПустоеЗначение(СтруктураПараметров.Данные.ТекстЗаголовка)=0 Тогда
		Форма.Заголовок(СтруктураПараметров.Данные.ТекстЗаголовка);
	КонецЕсли;
	
//ТипИконки - 0 - Information
//			  1 - Exclamation
//            2 - Error
//            3 - Delete
//            4 - Question
//            5 - Restricted
//            6 - Stop
	
	ИмяФайлаКартинки="";	

	Каталог=глСкин.КаталогСкина;

	Если ТипЗначенияСтр(СтруктураПараметров.Данные.ТипИконки)="Число" Тогда
		Если СтруктураПараметров.Данные.ТипИконки=1 Тогда
			ИмяФайлаКартинки=Каталог+"\Warning.bmp";	
		ИначеЕсли СтруктураПараметров.Данные.ТипИконки=2 Тогда
			ИмяФайлаКартинки=Каталог+"\Error.bmp";	
		ИначеЕсли СтруктураПараметров.Данные.ТипИконки=3 Тогда
			ИмяФайлаКартинки=Каталог+"\Delete.bmp";	
		ИначеЕсли СтруктураПараметров.Данные.ТипИконки=4 Тогда
			ИмяФайлаКартинки=Каталог+"\Question.bmp";	
		ИначеЕсли СтруктураПараметров.Данные.ТипИконки=5 Тогда
			ИмяФайлаКартинки=Каталог+"\Restricted.bmp";	
		ИначеЕсли СтруктураПараметров.Данные.ТипИконки=6 Тогда
			ИмяФайлаКартинки=Каталог+"\Stop.bmp";	
		ИначеЕсли СтруктураПараметров.Данные.ТипИконки=7 Тогда
			ИмяФайлаКартинки=Каталог+"\Check.bmp";	
		КонецЕсли;
	ИначеЕсли ТипЗначенияСтр(СтруктураПараметров.Данные.ТипИконки)="Строка" Тогда
		
		//Это имя файла
		ИмяФайлаКартинки=СтруктураПараметров.Данные.ТипИконки;
		Если ФС.СуществуетФайл(ИмяФайлаКартинки)=0 Тогда
			//нет такой картинки
			//пробуем найти в каталоге кнопок
			ИмяФайлаКартинки=Каталог+"\"+СтруктураПараметров.Данные.ТипИконки;
		
			Если ФС.СуществуетФайл(ИмяФайлаКартинки)=0 Тогда
				//не нашли - берем стандартную
				ИмяФайлаКартинки="";	
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИмяФайлаКартинки<>"" Тогда
		Иконка.Загрузить(ИмяФайлаКартинки);
	КонецЕсли;
	
	
	
	
	Если СлойИнфо="Многострочный" Тогда
		оФорма = СоздатьОбъект("Форма");
		оФорма.УстановитьФорму(Форма);
	
		
		ОбъектИнфо=оФорма.СоздатьЭлементУправления("ПолеТабличногоДокумента",Форма.кнТаблица);	
		
		ОбъектИнфо.Таблица.ИсходнаяТаблица("Таблица");
		ОбъектИнфо.Таблица.ВывестиСекцию("Шапка");
		
		ТекстСообщенияМнч=ТекстСообщения;
		Для Инд=1 По СтрКоличествоСтрок(ТекстСообщенияМнч) Цикл
			ТекстСообщения=СтрПолучитьСтроку(ТекстСообщенияМнч,Инд);
			ОбъектИнфо.Таблица.ВывестиСекцию("Сообщение");
		КонецЦикла;	
		
		
		
		ОбъектИнфо.Таблица.ТолькоПросмотр(1);
		ОбъектИнфо.Таблица.ПараметрыСтраницы(1,,,10,0,0,0,0,0,1);
		ОбъектИнфо.Таблица.Опции(0,0,0,0);
	Иначе
		ОбъектИнфо=Объект.ПолучитьОбъектПоИмени("тктИнфо");
		ОбъектИнфо.УстановитьТекст(ТекстСообщения);
		
	КонецЕсли;	
	
	Объект.ИспользоватьСлой("Основной,"+Слой+","+СлойИнфо,НомерКнопкиПоУмолчанию);
КонецПроцедуры

Процедура ПослеОткрытия()
	Объект.Кнопки.АвтоРисование(1);
КонецПроцедуры

Процедура ПриЗакрытии()
	
	Объект.УдалитьКнопки();
	
	Если ПоКнопке=0 Тогда
		Если СтруктураПараметров.Результат<>"Таймаут" Тогда
			СтруктураПараметров.Результат="Отмена";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


Процедура  ПриНажатииКнопкиКлавиатуры(пКодКлавиши,Alt,Shift,Ctrl,пСимвол,пФлагСтандартнойОбработки)
	
	Перем лКоманда,лКодКлавиши;

	Если ЗначениеТаймера>0 Тогда
		РасширениеФормыТаймер = СоздатьОбъект("РасширениеФормы");
		РасширениеФормыТаймер.ОбработкаОжидания("ОбновитьТаймер",0);
		Форма.тктТаймер.Видимость(0);
	КонецЕсли;
	
	Если (пКодКлавиши=kbUp)или(пКодКлавиши=kbLeft) или ((пКодКлавиши=kbTab)И(Shift=1)) Тогда	
		пФлагСтандартнойОбработки=0;
		Объект.Предыдущая();
	ИначеЕсли (пКодКлавиши=kbDown)или(пКодКлавиши=kbRight) или ((пКодКлавиши=kbTab)И(Shift=0)) Тогда	
		пФлагСтандартнойОбработки=0;
		Объект.Следующая();
		
	ИначеЕсли (пКодКлавиши=kbPgUp)или(пКодКлавиши=kbHome) Тогда	
		пФлагСтандартнойОбработки=0;
		Объект.Первая();
	ИначеЕсли (пКодКлавиши=kbPgDn)или(пКодКлавиши=kbEnd) Тогда	
		пФлагСтандартнойОбработки=0;
		Объект.Последняя();
	КонецЕсли;
	
КонецПроцедуры


Процедура кнОднаКнопка_Click()
    ВыполнитьВыбор("ОднаКнопка");
КонецПроцедуры       

Процедура кнЛевая_Click()
    ВыполнитьВыбор("Левая");
КонецПроцедуры       

Процедура кнПравая_Click()
    ВыполнитьВыбор("Правая");
КонецПроцедуры       

Процедура кнПервая_Click()
    ВыполнитьВыбор("Первая");
КонецПроцедуры       

Процедура кнВторая_Click()
    ВыполнитьВыбор("Вторая");
КонецПроцедуры       

Процедура кнТретья_Click()
    ВыполнитьВыбор("Третья");
КонецПроцедуры       

ПоКнопке=0;
ЗначениеТаймера=0;
СтароеВремя="";