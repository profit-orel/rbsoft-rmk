//Конфигурация для платформы "1С:Предприятие 7.7" РБ-Софт:Рабочее место кассира.
//Версия 4.43 от 19.07.10 20:54:19
//Назначение: Автоматизация POS-узлов в магазинах (Point of sale).
//(c) Баинов Евгений, ООО "РБ-Софт" 2006, г.Улан-Удэ, проспект 50-летия Октября,21А,офис 110 
//e-mail: bainov@rb-soft.ru  icq: 73718937, телефоны: (3012) 44-03-57, 44-76-42, 44-86-58
//---------------------------------------------------------------------------------------
//Данная программа является свободным программным обеспечением.
//Вы вправе распространять ее и/или модифицировать в соответствии с условиями версии 2 
//либо по вашему выбору с условиями более поздней версии Стандартной Общественной Лицензии GNU,
//опубликованной Free Software Foundation. 
//Мы распространяем данную программу в надежде на то, что она будет вам полезной, 
//однако НЕ ПРЕДОСТАВЛЯЕМ НА НЕЕ НИКАКИХ ГАРАНТИЙ, 
//в том числе ГАРАНТИИ ТОВАРНОГО СОСТОЯНИЯ ПРИ ПРОДАЖЕ
//и ПРИГОДНОСТИ ДЛЯ ИСПОЛЬЗОВАНИЯ В КОНКРЕТНЫХ ЦЕЛЯХ.
//Для получения более подробной информации ознакомьтесь со Стандартной Общественной Лицензией GNU.
//Вместе с данной программой вы должны были получить экземпляр Стандартной Общественной Лицензии GNU.
//Если вы его не получили, сообщите об этом в Free Software Foundation, Inc.,
//59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
//_______________________________________________________________________________________
//Имя модуля:
// <Вставить имя модуля>
//_______________________________________________________________________________________
//Разработчик: Баинов Евгений, e-mail: bainov@rb-soft.ru , icq: 73718937
//_______________________________________________________________________________________
//Назначение модуля:
// <Вставить описание модуля>
//
//_______________________________________________________________________________________
Перем _База Экспорт;
Перем ИД Экспорт;//уникальный идентификатор
Перем Данные Экспорт;//ссылка на структуру с данными
Перем ТолькоЧтение;

Функция Этот(Конт) Возврат Конт; КонецФункции

Функция Сам() Возврат Этот(Контекст) КонецФункции



Функция _ПолучитьКолвоДСвойств()  экспорт
	Возврат 4;
КонецФункции

Функция _ПолучитьИмяДСвойства (чНомерПар) экспорт
	Если чНомерПар=1 Тогда
		Возврат "_База";
	ИначеЕсли чНомерПар=2 Тогда
		Возврат "ИД";
	ИначеЕсли чНомерПар=3 Тогда
		Возврат "Данные";
	ИначеЕсли чНомерПар=4 Тогда
		Возврат "ТолькоЧтение";
	КонецЕсли;
КонецФункции

//------------------------------------ ------------------------------------
Процедура Конструктор()
	_База = Этот(Контекст).ПолучитьБазовыйКласс("Объект");
	Данные=глБД.Новый(Сам().Вид());
	GUID=СоздатьОбъект("GUID");
	GUID.Новый();
	ИД=GUID.ВСтроку();

	GUID=0;
	ТолькоЧтение=0;


	Попытка
		Данные.ИД=ИД;
	Исключение
	КонецПопытки;
КонецПроцедуры // Конструктор

Процедура Деструктор()
	Данные=0;
	//сообщить("Деструктор ОбъектБД");
КонецПроцедуры // Деструктор

//вызывается после успешного чтения объекта из базы

Процедура  ПослеЧтения() Экспорт

КонецПроцедуры

Функция Прочитать(_ИД) Экспорт
	Сам=Сам();
	Рез=глБД.ПрочитатьОбъект(Данные,Формат(_ИД,"С40"));
	Если Рез=1 Тогда
		Сам.ПослеЧтения();
		Возврат 1;
	КонецЕсли;
	Возврат 0;
КонецФункции	// Прочитать
                       
Функция Перечитать() Экспорт
	возврат Прочитать(Данные.ИД);
КонецФункции 
                       
//Сравнивает текущий объект с другим объектом
//возвращает 1 - если у объектов одинаковые идентификаторы.
//различия в правых пробелах игнорируются
Функция Сравнить(Объект) Экспорт         
	                
	Попытка
		Если СокрП(Данные.ИД)=СокрП(Объект.Данные.ИД) Тогда
			Возврат 1;
		КонецЕсли;   
			
	Исключение
	КонецПопытки;
	Возврат 0;
КонецФункции

//абстрактный метод
//можно определить какие-то действия перед записью объекта, наприме проверку корректности данных
//Если присвоить переменной Отказ значение 1, то объект не будет записан в базу
Процедура  ПередЗаписью(Отказ) Экспорт
	Отказ=0;
КонецПроцедуры
//абстрактный метод
//вызывается автоматически после успешной записи объекта
//переопределять если надо делать что-то после записи
Процедура  ПослеЗаписи() Экспорт

КонецПроцедуры
//абстрактный метод
//вызывается автоматически перед удалением объекта
//переопределять если надо делать что-то перед удалением
Процедура  ПередУдалением(Отказ) Экспорт

КонецПроцедуры


Функция Записать() Экспорт
	Сам=Сам();
	Отказ=0;
	Если Сам.РежимТолькоЧтение()=1 Тогда
		Сам.УстановитьКодИОшибку(4001,"Попытка записи объекта в режиме ""Только чтение""");
		возврат 0;
	КонецЕсли;
	Сам.ПередЗаписью(Отказ);
	Если Отказ=1 Тогда
		Возврат 0;
	ИначеЕсли глБД.ЗаписатьОбъект(Данные)=1 Тогда
		Данные.ЭтоНовый=0;

		Сам.ПослеЗаписи();
		возврат 1;
	КонецЕсли;
	возврат 0;
КонецФункции	// Записать

Функция Удалить() Экспорт
	Сам=Сам();
	Отказ=0;
	Если Сам.РежимТолькоЧтение()=1 Тогда
		Сам.УстановитьКодИОшибку(4001,"Попытка удаления объекта в режиме ""Только чтение""");
		возврат 0;
	КонецЕсли;
	Сам.ПередУдалением(Отказ);
	Если Отказ=1 Тогда
		Возврат 0;
	ИначеЕсли глБД.УдалитьОбъект(Сам)=1 Тогда
		возврат 1;
	КонецЕсли;
	возврат 0;
КонецФункции

Функция РежимТолькоЧтение(Зн = -1) Экспорт
	_Рез = ТолькоЧтение;
	Если Зн <> -1 Тогда ТолькоЧтение = ?(Зн = 0, 0, 1); КонецЕсли;
	Возврат _Рез;
КонецФункции

Функция ГруппировочноеПоле() Экспорт
	возврат "";
КонецФункции

Функция Представление() Экспорт
	Попытка
		Возврат Данные.Наименование;
	Исключение
		Попытка
			Возврат Данные.ИД;
		Исключение
			Возврат "<"+Сам().Вид()+">";
		КонецПопытки;
	КонецПопытки;
КонецФункции	// гл

//должна проверить правильность данных
//если данные неверные должна вернуть 0 и выставить код и описание ошибки
Функция Проверить() Экспорт
	возврат 1;
КонецФункции


//возвращает 1 - выбор сделан
//			 0 - выбор не сделан
//Данные- объект, который будет выбран
//        Если является объектом, то будет попытка  спозиционироваться на нем при открытии
//        Если множественный выбор, то вернется список значений с выбранными объектами
//РежимВыбора -
// 1 - выбор одного элемента,
// 2 - множественный выбор,
//ИмяФормыСписка - имя формы, которую надо открыть
//                 считается, что форма находится в обработке "Class_"+ВидОбъекта+"_"+ИмяФормыСписка
//                 Если ИмяФормыСписка="ФормаСписка" и объект "Товар", то откроется форма обработки "Class_Товар_ФормаСписка

Функция ВыбратьОбъект(Данные="",РежимВыбора=1,ИмяФормыСписка="ФормаСписка") Экспорт
	Сам=Сам();
	Структура=СоздатьОбъект("Структура");
	Структура.Вставить("Результат",0);
	Структура.Вставить("РежимВыбора",РежимВыбора);
	Структура.Вставить("Объект",Сам);
	Структура.Вставить("Данные",Данные);


	ИмяОбработки=Сам.ПолучитьИмяОткрываемойФормы(ИмяФормыСписка);


	//Попытка
	//	МД=Метаданные.Обработка(ИмяОбработки);
	//	ИД=МД.Идентификатор;
	//Исключение
	//	ИмяОбработки="Class_ОбъектБД_ФормаСписка";
	//КонецПопытки;

	Попытка
		ОткрытьФормуМодально(ИмяОбработки,Структура);
	Исключение
		глДебаг(ОписаниеОшибки(),"Функция ОбъектБД::ВыбратьОбъект");
		глПредупреждение(ОписаниеОшибки());
		Возврат 0;
	КонецПопытки;


	Результат=Структура.Результат;
	Если Результат=1 Тогда
		Данные=Структура.Данные;
	КонецЕсли;
	возврат Результат;
КонецФункции
//


Функция НайтиПоКоду(Код) Экспорт
	Сам=Сам();
	Возврат глБД.ПолучитьОбъектПоИД(Сам.Вид(),Код);
КонецФункции

//------------------------------------ ------------------------------------
Процедура ПриОткрытии()

//#if _NOW_PREPARE_CLASS
	Форма.Параметр._ПриОткрытии(); // для отладки класса
//#endif // _NOW_PREPARE_CLASS
КонецПроцедуры // ПриОткрытии



Функция _ПолучитьКод () Экспорт
	Сам=Сам();
	_ИД="Класс."+Сам.Вид()+"/"+СокрЛП(Сам.ИД);
	Попытка
		_ИД=_ИД+"/"+Сам.Данные.Наименование;
	Исключение
	КонецПопытки;

	Возврат _ИД;
КонецФункции