//Конфигурация для платформы "1С:Предприятие 7.7" РБ-Софт:Рабочее место кассира.
//Версия 4.43 от 19.07.10 20:54:20
//Назначение: Автоматизация POS-узлов в магазинах (Point of sale).
//(c) Баинов Евгений, ООО "РБ-Софт" 2006, г.Улан-Удэ, проспект 50-летия Октября,21А,офис 110 
//e-mail: bainov@rb-soft.ru  icq: 73718937, телефоны: (3012) 44-03-57, 44-76-42, 44-86-58
//---------------------------------------------------------------------------------------
//Данная программа является свободным программным обеспечением.
//Вы вправе распространять ее и/или модифицировать в соответствии с условиями версии 2 
//либо по вашему выбору с условиями более поздней версии Стандартной Общественной Лицензии GNU,
//опубликованной Free Software Foundation. 
//Мы распространяем данную программу в надежде на то, что она будет вам полезной, 
//однако НЕ ПРЕДОСТАВЛЯЕМ НА НЕЕ НИКАКИХ ГАРАНТИЙ, 
//в том числе ГАРАНТИИ ТОВАРНОГО СОСТОЯНИЯ ПРИ ПРОДАЖЕ
//и ПРИГОДНОСТИ ДЛЯ ИСПОЛЬЗОВАНИЯ В КОНКРЕТНЫХ ЦЕЛЯХ.
//Для получения более подробной информации ознакомьтесь со Стандартной Общественной Лицензией GNU.
//Вместе с данной программой вы должны были получить экземпляр Стандартной Общественной Лицензии GNU.
//Если вы его не получили, сообщите об этом в Free Software Foundation, Inc.,
//59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
//_______________________________________________________________________________________
//Имя модуля:
// <Вставить имя модуля>
//_______________________________________________________________________________________
//Разработчик: Баинов Евгений, e-mail: bainov@rb-soft.ru , icq: 73718937
//_______________________________________________________________________________________
//Назначение модуля:
// <Вставить описание модуля>
//
//_______________________________________________________________________________________
Перем АтрДерево;

Перем Объект;
Перем НомСтр;
Перем тзЭлемента;


Перем ТабЗначения;
Перем ТекстКомментария;
Перем ТекущаяСтрокаКомментария;


//В дерево добавляем строку
Процедура ДобавитьСтрокуДерева(ТЗ,ОбъектДерева,Представление="",Картинка=0,ТЗПодуровень=0) Экспорт

	ТЗ.НоваяСтрока();
	ТЗ.Подуровень=ТЗПодуровень;
	ТЗ.Картинка=Картинка;
	ТЗ.Представление = Представление;
	ТЗ.Объект=ОбъектДерева; //сюда пишем ссылку на объект
КонецПроцедуры	// ДобавитьСтрокуДерева

Функция СоздатьТЗДерева() Экспорт

	ТЗ=СоздатьОбъект("ТаблицаЗначений");
	ТЗ.НоваяКолонка("Подуровень"); //ТЗ с такой же структурой
	ТЗ.НоваяКолонка("Картинка","Число");
	ТЗ.НоваяКолонка("Представление","Строка",,,"",100);
	ТЗ.НоваяКолонка("Объект");//сюда пишем описание объекта в виде структуры
	ТЗ.ВидимостьКолонки("Объект",0);
	возврат ТЗ;
КонецФункции	// СоздатьТЗДерева

Процедура ИнициализацияДерева()
	НаименованиеСекции="";
	Параметр          ="";
	Значение          ="";
	СсылкаНаСекцию="";
	ИмяСекции="";
	ТЗ=СоздатьТЗДерева();
	ТЗ.Выгрузить(ТабРаздел);
	ТабРаздел.ВидимостьКолонки("Объект",0);

	Объект.ВыбратьСвойства();
	Пока Объект.ПолучитьСвойство(СсылкаНаСекцию,ИмяСекции)=1 Цикл
		//Получим наименование секции
		СсылканаПоле="";
		ИмяПоля="";
		ТабПараметров    = СоздатьТЗДерева();
		СсылкаНаСекцию.ВыбратьСвойства();
		Пока СсылкаНаСекцию.ПолучитьСвойство(СсылканаПоле,ИмяПоля)=1 Цикл
			Если ИмяПоля="_Комментарий" Тогда

			ИначеЕсли ИмяПоля="_ИД" Тогда

			Иначе
				ДобавитьСтрокуДерева (ТабПараметров,СсылканаПоле,ИмяПоля+" = "+СсылканаПоле.Значение);
			КонецЕсли;

		КонецЦикла;

		ДобавитьСтрокуДерева (ТабРаздел,СсылкаНаСекцию,ИмяСекции,1,ТабПараметров);

		СтрокаПолей="";
	КонецЦикла;
	АтрДерево.УстановитьАтрибут(Форма, "ТабРаздел");
	АтрДерево.ПерехватитьТаблицуЗначений();
	АтрДерево.ОпцииДерева  (0,0,1);
КонецПроцедуры



Функция ПолучитьТекущийОбъект()
	НомСтр          = АтрДерево.ТекущаяСтрокаДерева();
	ОбъектДерева    = АтрДерево.ЗначениеИзДерева(НомСтр,4);
	Возврат ОбъектДерева;
КонецФункции

Процедура ОбновлениеКомментария()
	Попытка
		НомСтр          = АтрДерево.ТекущаяСтрокаДерева();
		Если ТекущаяСтрокаКомментария =НомСтр Тогда
			Возврат;
		Иначе
			ТекущаяСтрокаКомментария=НомСтр;
			ОбъектДерева=ПолучитьТекущийОбъект();
			ТекстКомментария=ОбъектДерева._Комментарий;
		КонецЕсли;
	Исключение
	КонецПопытки;
КонецПроцедуры

Процедура РедактироватьПараметр()

	ОбъектДерева=ПолучитьТекущийОбъект();
	ИДОбъекта       = ОбъектДерева._ИД;
    Если ТипЗначенияСтр(ОбъектДерева)="Структура" Тогда
		//в Инифайле это параметр секции
		ЗначениеЭлемента=ОбъектДерева.Значение;
		Если ВвестиСтроку(ЗначениеЭлемента,"Введите параметр "+ИДОбъекта,100)=1 Тогда
			ОбъектДерева.Значение=ЗначениеЭлемента;
			НомСтр          = АтрДерево.ТекущаяСтрокаДерева();
			Рез=АтрДерево.ЗначениеВДерево(НомСтр,3,ОбъектДерева._ИД+" = "+ОбъектДерева.Значение);
		КонецЕсли;  //ВвестиСтроку()=1
	КонецЕсли;
КонецПроцедуры

Процедура ПереименоватьПараметр()
	ОбъектДерева   = ПолучитьТекущийОбъект();
	ИДОбъекта= ОбъектДерева._ИД;
	НовыйИД  = ИДОбъекта;

	Если ВвестиСтроку(НовыйИД,"Переименование",100)=1 Тогда

		НовыйИД=СокрЛП(НовыйИД);
		НовыйИД=СтрЗаменить(НовыйИД," ","_");
		ОбъектДерева._ИД=НовыйИД;
		НомСтр = АтрДерево.ТекущаяСтрокаДерева();

		Поз=Найти(НомСтр,"/");



		Если Поз=0 Тогда


			//Это секция
			НомерСекции=Число(НомСтр);
			НомерПараметра=0;
			ТабРаздел.УстановитьЗначение(НомерСекции,3,НовыйИД);
			Объект.ПереименоватьСекцию(ИДОбъекта,НовыйИД);
		Иначе
			НомерСекции=Число(Лев(НомСтр,Поз-1));
			НомерПараметра=Число(Сред(НомСтр,Поз+1));

			ТЗПараметров=ТабРаздел.ПолучитьЗначение(НомерСекции,1);
			ИмяСекции=ТабРаздел.ПолучитьЗначение(НомерСекции,2);
			ТЗПараметров.УстановитьЗначение(НомерПараметра,3,НовыйИД);

			Объект.ПереименоватьПараметр(ИмяСекции+"/"+ИДОбъекта,ИмяСекции+"/"+НовыйИД);

		КонецЕсли;

		Если ТипЗначенияСтр(ОбъектДерева)="Структура" Тогда
			Рез=АтрДерево.ЗначениеВДерево(НомСтр,3,ОбъектДерева._ИД+" = "+ОбъектДерева.Значение);
		Иначе
			Рез=АтрДерево.ЗначениеВДерево(НомСтр,3,ОбъектДерева._ИД);
		КонецЕсли;
	КонецЕсли;  //ВвестиСтроку()=1
КонецПроцедуры

Процедура ПриНажатииНаДереве()
	ОбновлениеКомментария();
	РедактироватьПараметр();
КонецПроцедуры	// ПриНажатииНаДереве

Процедура кнОК()
	ОбъектДерева          = ПолучитьТекущийОбъект();
	ОбъектДерева._Комментарий = Комментарий;
	ТекстКомментария = Комментарий;
	Форма.ИспользоватьСлой("Основной");

КонецПроцедуры

Процедура кнНазад()
	Форма.ИспользоватьСлой("Основной");
    ОбновлениеКомментария();
КонецПроцедуры

Процедура Записать()
	Если Объект.Записать()=0 Тогда
		глПредупреждение("Не удалось записать в файл.
		|Ошибка    :"+Объект.ПоследняяОшибка());

	КонецЕсли;
КонецПроцедуры

Процедура СоздатьСекцию()
	//Добавим секцию

	ИмяСекции="";
	Если ВвестиСтроку(ИмяСекции,"Введите наименование новой секции",100)<>1 Тогда
		Возврат;
	КонецЕсли;  //ВвестиСтроку()=1

	Если Объект.СекцияСуществует(ИмяСекции)=1 Тогда
		глПредупреждение("Секция с таким именем уже существует!");
	Иначе
		Секция=Объект.СоздатьСекцию(ИмяСекции);


		ДобавитьСтрокуДерева(ТабРаздел,Секция,ИмяСекции,,СоздатьТЗДерева());
		АтрДерево.ОбновитьДерево();
	КонецЕсли;

КонецПроцедуры

Процедура СоздатьПараметр()
    //Обработаем строку дерева

	НомСтр          = АтрДерево.ТекущаяСтрокаДерева();
	Если ПустоеЗначение(НомСтр)=1 Тогда
		глПредупреждение("Не выбрана ветка для добавления параметра!!!",3);
		Возврат;
	КонецЕсли;
	Текст = СоздатьОбъект("Текст");
	Текст.ДобавитьСтроку(СтрЗаменить(НомСтр,"/",РазделительСтрок));
	НомерСекции=Текст.ПолучитьСтроку(1);

	//Введём название параметра...
	ИмяПараметра="";
	Если ВвестиСтроку(ИмяПараметра,"Введите наименование нового параметра",100)<>1 Тогда
		Возврат;
	КонецЕсли;


	//Получим Обьект элемента
	СсылкаНаСекцию= АтрДерево.ЗначениеИзДерева(НомерСекции,4);
    //Получили обьект теперь добавим туда новый параметр
	СсылкаНаПоле=Объект.СоздатьПоле(ИмяПараметра,"");

	Попытка
		СсылкаНаСекцию.ДобавитьСвойство(ИмяПараметра,СсылкаНаПоле);
	Исключение
		глПредупреждение(""+ОписаниеОшибки());
		Возврат;
	КонецПопытки;


	//Записали данные в обьект теперь отобразим это на экране
	//Получим ТЗ из Секции
	тзПараметра =  ТабРаздел.ПолучитьЗначение(Число(НомерСекции),1);

    //Установим узел для добавления параметра
	АтрДерево.УстановитьТекущийУзел(НомерСекции);
	//Добавим данные в Дерево-Значений
	ДобавитьСтрокуДерева(тзПараметра,СсылкаНаПоле,СсылкаНаПоле._ИД+" = ");
	АтрДерево.ОбновитьДерево();
	АтрДерево.РазвернутьУзел(НомерСекции);
КонецПроцедуры

Процедура Удалить()

	НомСтр          = АтрДерево.ТекущаяСтрокаДерева();
	ОбъектДерева=ПолучитьТекущийОбъект();
	ИДОбъекта=    ОбъектДерева._ИД;



	ОбъектДерева=0;
	Текст = СоздатьОбъект("Текст");
	Текст .ДобавитьСтроку(СтрЗаменить(НомСтр,"/",РазделительСтрок));
	НомерСекции=Число(Текст.ПолучитьСтроку(1));
	ИмяСекции= АтрДерево.ЗначениеИзДерева(""+НомерСекции,3);


	Если Текст.КоличествоСтрок()=1 Тогда

		Если глВопрос("Удалить секцию ["+ИмяСекции+"] ?","Да+Нет")="Да" Тогда

			//Удалим Секцию
			ТабРаздел.УдалитьСтроку(НомерСекции);
			Объект.УдалитьСекцию(ИмяСекции);
			АтрДерево.ОбновитьДерево();
		КонецЕсли;
	Иначе
		//Удалим параметр
		//Здесь для начала необходимо получить ТЗ из ТабРаздела
		НомерПараметра=Число(Текст.ПолучитьСтроку(2));
		ИмяПараметра=ИДОбъекта;

		Если глВопрос("Удалить параметр "+ИмяПараметра+" в секции ["+ИмяСекции+"] ?","Да+Нет")="Да" Тогда

			тзУдРаздела =  ТабРаздел.ПолучитьЗначение(НомСтр,1);
			тзУдРаздела.УдалитьСтроку(НомерПараметра);
			Объект.УдалитьПараметр(ИмяСекции+"/"+ИмяПараметра);
			//...Обновим структуру дерева...
			АтрДерево.ОбновитьДерево();
			АтрДерево.РазвернутьУзел(НомерСекции);
		КонецЕсли;
	КонецЕсли;

	ОбновлениеКомментария();
КонецПроцедуры



Процедура ПриНажатииЛевойКнопкиНадДеревом(НомерСтроки, НомерКолонки)
     ОбновлениеКомментария();
КонецПроцедуры




Процедура ИзменитьКомментарий()
	ОбъектДерева=ПолучитьТекущийОбъект();
	Комментарий=ОбъектДерева._Комментарий ;
	Форма.ИспользоватьСлой("Комментарий",2);
	Форма.Обновить();
КонецПроцедуры

Процедура ПриНажатииПравойКнопкиНадДеревом(НомерСтроки, НомерКолонки)


	 АтрДерево.УстановитьТекущийУзел(НомерСтроки);
	 ОбновлениеКомментария();

     Сервис = СоздатьОбъект("Сервис");
	 сз=СоздатьОбъект("СписокЗначений");


	 сз.ДобавитьЗначение("Изменить");
	 сз.ДобавитьЗначение("Изменить комментарий");
	 сз.ДобавитьЗначение("Переименовать");
	 сз.ДобавитьЗначение("Добавить секцию");
	 сз.ДобавитьЗначение("Добавить параметр");
	 сз.ДобавитьЗначение("Удалить");
	 Выб = "";
	 Если Сервис.ВыбратьЗначение(Сз,Выб,"",0) = 1 Тогда
	 	Если Выб="Изменить" Тогда
	 		РедактироватьПараметр();
	 	ИначеЕсли Выб="Переименовать" Тогда
	 		ПереименоватьПараметр();
	 	ИначеЕсли Выб="Добавить секцию" Тогда
	 		СоздатьСекцию();
	 	ИначеЕсли Выб="Добавить параметр" Тогда
	 		СоздатьПараметр();
	 	ИначеЕсли Выб="Изменить комментарий" Тогда
	 	    ИзменитьКомментарий();
	 	ИначеЕсли Выб="Удалить" Тогда
	 	    Удалить();
	 	КонецЕсли;
	 КонецЕсли;


КонецПроцедуры




Процедура ПриОткрытии()

	Если ПустоеЗначение(Форма.Параметр)=1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	СтруктураПараметров=Форма.Параметр;
	Объект=СтруктураПараметров.Объект;


	Форма.Заголовок("Редактор ини-файлов:"+Объект.ИмяФайла);
	Форма.ИспользоватьСлой("Основной");
КонецПроцедуры

Процедура ПослеОткрытия()
	ФормаРасш = СоздатьОбъект("РасширениеФормы");
	ФормаРасш.УстановитьФорму(Форма);
	АтрДерево     =СоздатьОбъект("АтрибутФормы");
	ИнициализацияДерева();
	Активизировать("ТабРаздел");
	АтрДерево.УстановитьТекущийУзел(1);

	ОбновлениеКомментария();
КонецПроцедуры

ТекущаяСтрокаКомментария=""