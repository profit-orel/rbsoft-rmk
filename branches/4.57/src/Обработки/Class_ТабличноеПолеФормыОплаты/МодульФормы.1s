//Конфигурация для платформы "1С:Предприятие 7.7" РБ-Софт:Рабочее место кассира.
//Версия 4.57 от 05.06.11 23:24:52
//(c) Баинов Евгений, ООО "РБ-Софт" 2006-2011 г.Улан-Удэ, ул.Балтахинова,17Е,офис 215
//e-mail: bainov@rb-soft.ru  icq: 73718937, телефоны: (3012) 440-357, 278-228
//Сайт проекта: http://code.google.com/p/rbsoft-rmk/
//_______________________________________________________________________________________
//Класс ТабличноеПолеФормыОплаты
//_______________________________________________________________________________________
Перем _База Экспорт;
Перем ШрифтЗаголовка Экспорт;
Перем ШрифтОсновной Экспорт;

Перем ЦветТекстаЗаголовка Экспорт;
Перем ЦветФонаЗаголовка Экспорт;
Перем ТаблицаОбъектов Экспорт;

Перем РМК Экспорт;

Перем ИндексКартинкиОплатаПроведена;
Перем ИндексКартинкиОплатаНеПроведена;

Функция Этот(Конт) Возврат Конт; КонецФункции
Функция Сам() Возврат Этот(Контекст) КонецФункции



Процедура НастройкаПолей()  Экспорт


		Сам=Сам();

		Сам.Колонки.Добавить("Картинка");
		Сам.Колонки.Добавить("НомерОперации");
		Сам.Колонки.Добавить("ВидОплаты");
		Сам.Колонки.Добавить("Сумма");

		Сам.ВертСкроллер=0;
		Сам.РежимВыделенияСтроки=1;
		Сам.ВертикальныеЛинии=1;
		Сам.ГоризонтальныеЛинии=1;
		Сам.СтильЗаголовков=0;
		Сам.ТаймаутБыстрогоПоиска=1;
		Сам.ТаймаутОбновления=0;
		Сам.ИзменятьПозициюКолонок=0;
		Сам.РазрешитьНачалоПеретаскивания=0;
		Сам.РазрешитьПеретаскивание=0;
		Сам.ЧередованиеЦветовСтрок=1;

		Сам.ЦветФонаЧередованияСтрок=глОбщиеФункции.ПолучитьЦвет(250,240,240);

		ЦветФонаЗаголовка=глОбщиеФункции.ПолучитьЦвет(255,250,190);
		ЦветТекстаЗаголовка=глОбщиеФункции.ПолучитьЦвет(190,185,125);

		Сам.ЦветФона=глОбщиеФункции.ПолучитьЦвет(255,255,255);
		Сам.ЦветТекста=глОбщиеФункции.ПолучитьЦвет(0,0,0);
		Сам.ЦветЛиний=глОбщиеФункции.ПолучитьЦвет(200,200,200);
		Сам.Шрифт=ШрифтОсновной;





		Картинки=СоздатьОбъект("Картинка");
		Картинки.Загрузить("СтатусыУтверждения");

		Сам.Колонки.Картинка.Ширина=3;
		Сам.Колонки.Картинка.УстановитьКартинкиСтрок(Картинки);


		Сам.Колонки.НомерОперации.Данные="НомерОперации";
		Сам.Колонки.НомерОперации.Видимость=1;
		Сам.Колонки.НомерОперации.ГоризонтальноеВыравнивание=2;
		Сам.Колонки.НомерОперации.Ширина=5;
		Сам.Колонки.ВидОплаты.Заголовок="#";

		Сам.Колонки.ВидОплаты.Данные="Наименование";
		Сам.Колонки.ВидОплаты.ГоризонтальноеВыравнивание=1;
		Сам.Колонки.ВидОплаты.Ширина=50;
		Сам.Колонки.ВидОплаты.Заголовок="Вид оплаты";
		Сам.Колонки.ВидОплаты.Видимость=1;
		Сам.Колонки.ВидОплаты.ШрифтЗаголовка=ШрифтЗаголовка;


		Сам.Колонки.Сумма.Данные="Сумма";
		Сам.Колонки.Сумма.ГоризонтальноеВыравнивание=2;
		Сам.Колонки.Сумма.Ширина=10;
		Сам.Колонки.Сумма.Заголовок="Сумма";
		Сам.Колонки.Сумма.Видимость=1;
		Сам.Колонки.Сумма.ШрифтЗаголовка=ШрифтЗаголовка;



КонецПроцедуры	// НастройкаПолей


Процедура НастройкаШрифтов() Экспорт
	ШрифтОсновной 	= глФабрикаОбъектов.Шрифт("Arial", 14, 1);
	ШрифтЗаголовка 	= глФабрикаОбъектов.Шрифт("Arial", 10, 1);


КонецПроцедуры	// НастройкаШрифтов


Процедура ПриВыводеСтроки(ОформлениеСтроки, ДанныеСтроки) Экспорт
	Сам=Сам();

	ОформлениеСтроки.Ячейки.Картинка.ОтображатьКартинку=1;
	ОформлениеСтроки.Ячейки.Картинка.ОтображатьТекст=1;
	ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки=0;

	ОформлениеСтроки.Ячейки.Сумма.УстановитьТекст(глФРМ(ДанныеСтроки.Сумма));


	Ячейка = ОформлениеСтроки.Ячейки.Получить("Картинка");
	Ячейка.ОтображатьКартинку = 1;
	Ячейка.ОтображатьТекст = 0;
	Ячейка.Текст="";


	Если ДанныеСтроки.Сумма>0 Тогда
		Если ПустаяСтрока(ДанныеСтроки.Объект.Данные.ПлатежнаяСистема)=0 Тогда
			Если ДанныеСтроки.ОплатаПроведена=1 Тогда
				Ячейка.ИндексКартинки=ИндексКартинкиОплатаПроведена;
			Иначе
				Ячейка.ИндексКартинки=ИндексКартинкиОплатаНеПроведена;
			КонецЕсли;
		Иначе
			Ячейка.ИндексКартинки=ИндексКартинкиОплатаПроведена;
		КонецЕсли;
	Иначе
		Ячейка.ИндексКартинки=ИндексКартинкиОплатаНеПроведена;
	КонецЕсли;

КонецПроцедуры


Функция Инит(КонтекстФормы, ИдентификаторРеквизита,_РМК) Экспорт

	_База.Инит(КонтекстФормы, ИдентификаторРеквизита);

	ПД_ТаблицаОбъектов=СоздатьОбъект("ПоставщикДанныхТЗ");
	ПД_ТаблицаОбъектов.УстТаблицуЗначений(ТаблицаОбъектов);

	Сам().ПоставщикДанных=ПД_ТаблицаОбъектов;

	Сам=Сам();
	Сам.НастройкаШрифтов();
	Сам.НастройкаПолей();

	РМК=_РМК;

	Сам.Обновить();
	Возврат 1;
КонецФункции


Процедура Обновить() Экспорт
	Сам=Сам();

	ТаблицаОбъектов.УдалитьСтроки();

	ТЗ=СоздатьОбъект("ТаблицаЗначений");

	ТЗ=глБД.ПрочитатьОбъекты("ВидОплаты");


	ОплатаНаличными="";

	ТЗ.ВыбратьСтроки();
	Пока ТЗ.ПолучитьСтроку() = 1 Цикл
		Если ВРЕГ(ТЗ.Объект.Данные.Наименование)="НАЛИЧНЫЕ" Тогда
			ОплатаНаличными=ТЗ.Объект;
		Иначе
			ТаблицаОбъектов.НоваяСтрока();
			ТаблицаОбъектов.Объект=ТЗ.Объект;
			ТаблицаОбъектов.ИД=ТЗ.Объект.Данные.ИД;
			ТаблицаОбъектов.Наименование=ТЗ.Объект.Данные.Наименование;
			ТаблицаОбъектов.Сумма=0;
			ТаблицаОбъектов.КодФирмы=ТаблицаОбъектов.Объект.Данные.КодФирмы;


		КонецЕсли;
	КонецЦикла;
	ТаблицаОбъектов.Сортировать("Наименование");
	Если ПустоеЗначение(ОплатаНаличными)=0 Тогда
		ТаблицаОбъектов.НоваяСтрока(1);
		ТаблицаОбъектов.Объект=ОплатаНаличными;
		ТаблицаОбъектов.ИД=ОплатаНаличными.Данные.ИД;
		ТаблицаОбъектов.Наименование=ОплатаНаличными.Данные.Наименование;
		ТаблицаОбъектов.Сумма=0;
		ТаблицаОбъектов.КодФирмы=ОплатаНаличными.Данные.КодФирмы;
	КонецЕсли;

	РМК.ТаблицаОплаты.ВыбратьСтроки();
	Пока РМК.ТаблицаОплаты.ПолучитьСтроку()=1 Цикл

		ТаблицаОбъектов.ВыбратьСтроки();
		Пока ТаблицаОбъектов.ПолучитьСтроку() = 1 Цикл

			Если РМК.ТаблицаОплаты.ИД=ТаблицаОбъектов.ИД Тогда
				ТаблицаОбъектов.Сумма=РМК.ТаблицаОплаты.Сумма;
				ТаблицаОбъектов.ОплатаПроведена=РМК.ТаблицаОплаты.ОплатаПроведена;
				ТаблицаОбъектов.НомерОперации=РМК.ТаблицаОплаты.НомерОперации;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	Сам.ОбновитьСтроки();
КонецПроцедуры	//



Процедура Конструктор()
	_База = Этот(Контекст).ПолучитьБазовыйКласс("ТабличноеПолеСНастройками");
	Сам=Сам();
	ТаблицаОбъектов=НовыйОбъект("ТаблицаОплатыРМК");

	ИндексКартинкиОплатаПроведена=3;
	ИндексКартинкиОплатаНеПроведена=1;
КонецПроцедуры	// Конструктор




//вызывается посредством подписки на событие
//1 - можно продолжать обработку события другими обработчиками
//
Функция ВнешнееСобытие(Издатель,Событие) Экспорт
	Сам=Сам();

	Отработали=0;



	Если Событие.Источник="Команда" Тогда

		Команда=Событие.Описатель.Команда;


		Если Команда="ЗапросТекущейСтрокиОплаты" Тогда


			Если ТипЗначенияСтр(Сам.ТекущаяСтрока)="Число" Тогда
				Событие.Описатель.Ответ=Сам.ТекущаяСтрока;
			Иначе
				Событие.Описатель.Ответ=0;
			КонецЕсли;

			Отработали = 1;
		ИначеЕсли Команда="ПолучитьСтрокуОплаты" Тогда

			Если ТипЗначенияСтр(Сам.ТекущаяСтрока)="Число" Тогда
				Если ТаблицаОбъектов.КоличествоСтрок()>0 Тогда
					ТаблицаОбъектов.ПолучитьСтрокуПоНомеру(Сам.ТекущаяСтрока);

					Структура=СоздатьОбъект("Структура");
					Структура.Вставить("Сумма",ТаблицаОбъектов.Сумма);
					Структура.Вставить("ВидОплаты",ТаблицаОбъектов.Объект);
					Структура.Вставить("ОплатаПроведена",ТаблицаОбъектов.ОплатаПроведена);
					Структура.Вставить("НомерОперации",ТаблицаОбъектов.НомерОперации);

					Событие.Описатель.Ответ=Структура;
					Отработали = 1;
				КонецЕсли;
			КонецЕсли;


		ИначеЕсли Команда="ВНИЗ" Тогда

			Если ТипЗначенияСтр(Сам.ТекущаяСтрока)="Число" Тогда
				Сам.ТекущаяСтрока=Сам.ТекущаяСтрока+1;

				Если ТаблицаОбъектов.КоличествоСтрок()>0 Тогда
					ТаблицаОбъектов.ПолучитьСтрокуПоНомеру(Сам.ТекущаяСтрока);
					глПослатьКоманду(Сам,"УстановитьСтрокуРедактора",ТаблицаОбъектов.Сумма);


				КонецЕсли;
			КонецЕсли;
			Отработали = 1;
		ИначеЕсли Команда="ВВЕРХ" Тогда
			Если ТипЗначенияСтр(Сам.ТекущаяСтрока)="Число" Тогда
				Если Сам.ТекущаяСтрока>1 Тогда
					Сам.ТекущаяСтрока=Сам.ТекущаяСтрока-1;
					Если ТаблицаОбъектов.КоличествоСтрок()>0 Тогда
						ТаблицаОбъектов.ПолучитьСтрокуПоНомеру(Сам.ТекущаяСтрока);
						глПослатьКоманду(Сам,"УстановитьСтрокуРедактора",ТаблицаОбъектов.Сумма);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;

			Отработали = 1;
		ИначеЕсли Команда="В_НАЧАЛО" Тогда
			Если ТипЗначенияСтр(Сам.ТекущаяСтрока)="Число" Тогда
				Если Сам.ТекущаяСтрока>1 Тогда
					Сам.ТекущаяСтрока=1;

					Если ТаблицаОбъектов.КоличествоСтрок()>0 Тогда
						ТаблицаОбъектов.ПолучитьСтрокуПоНомеру(Сам.ТекущаяСтрока);
						глПослатьКоманду(Сам,"УстановитьСтрокуРедактора",ТаблицаОбъектов.Сумма);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;

			Отработали = 1;
		ИначеЕсли Команда="В_КОНЕЦ" Тогда
			Если ТипЗначенияСтр(Сам.ТекущаяСтрока)="Число" Тогда
				Сам.ТекущаяСтрока=Сам.ПоставщикДанных.ТаблицаЗначений.КоличествоСтрок();
				Отработали = 1;

				Если ТаблицаОбъектов.КоличествоСтрок()>0 Тогда
					ТаблицаОбъектов.ПолучитьСтрокуПоНомеру(Сам.ТекущаяСтрока);
					глПослатьКоманду(Сам,"УстановитьСтрокуРедактора",ТаблицаОбъектов.Сумма);
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Команда="УстановитьСтрокуОплаты" Тогда

			Если ТипЗначенияСтр(Сам.ТекущаяСтрока)="Число" Тогда
				Сам.ПоставщикДанных.ТаблицаЗначений.ПолучитьСтрокуПоНомеру(Сам.ТекущаяСтрока);
				Сам.ПоставщикДанных.ТаблицаЗначений.Сумма=Число(Событие.Описатель.Данные);

				ВидОплаты=Сам.ПоставщикДанных.ТаблицаЗначений.Объект;

				Событие.Описатель.Ответ=ВидОплаты;

				Сам.ОбновитьСтроки();

				Отработали = 1;


			КонецЕсли;


		ИначеЕсли Команда="УстановитьСтрокуОплаченной" Тогда
			Если ТипЗначенияСтр(Сам.ТекущаяСтрока)="Число" Тогда
				Сам.ПоставщикДанных.ТаблицаЗначений.ПолучитьСтрокуПоНомеру(Сам.ТекущаяСтрока);
				Сам.ПоставщикДанных.ТаблицаЗначений.ОплатаПроведена=1;

				ВидОплаты=Сам.ПоставщикДанных.ТаблицаЗначений.Объект;

				Событие.Описатель.Ответ=ВидОплаты;

				Сам.ОбновитьСтроки();

				Отработали = 1;


			КонецЕсли;
		ИначеЕсли Команда="УстановитьСтрокуНеоплаченной" Тогда
			Если ТипЗначенияСтр(Сам.ТекущаяСтрока)="Число" Тогда
				Сам.ПоставщикДанных.ТаблицаЗначений.ПолучитьСтрокуПоНомеру(Сам.ТекущаяСтрока);
				Сам.ПоставщикДанных.ТаблицаЗначений.ОплатаПроведена=0;

				ВидОплаты=Сам.ПоставщикДанных.ТаблицаЗначений.Объект;

				Событие.Описатель.Ответ=ВидОплаты;

				Сам.ОбновитьСтроки();

				Отработали = 1;


			КонецЕсли;

		КонецЕсли;



	КонецЕсли;

	Если Отработали = 1 Тогда
		глСобытиеОбработано(Событие);
		Сам.ОбновитьСтроки();


		глПослатьКоманду(Сам,"Обновить");
		Возврат 0;
    КонецЕсли;
	Возврат 1;
КонецФункции

Процедура Сброс() Экспорт
	Сам=Сам();
	ТаблицаОбъектов.Заполнить(0,,,"Сумма");
	Сам.ОбновитьСтроки();

КонецПроцедуры

Процедура ПриОткрытии()
//#if _NOW_PREPARE_CLASS
	Форма.Параметр._ПриОткрытии(); // для отладки класса
//#endif // _NOW_PREPARE_CLASS
КонецПроцедуры // ПриОткрытии