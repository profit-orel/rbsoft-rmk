//Конфигурация для платформы "1С:Предприятие 7.7" РБ-Софт:Рабочее место кассира.
//Версия 4.57 от 05.06.11 23:24:52
//(c) Баинов Евгений, ООО "РБ-Софт" 2006-2011 г.Улан-Удэ, ул.Балтахинова,17Е,офис 215
//e-mail: bainov@rb-soft.ru  icq: 73718937, телефоны: (3012) 440-357, 278-228
//Сайт проекта: http://code.google.com/p/rbsoft-rmk/
//_______________________________________________________________________________________
//Класс Кнопки
//_______________________________________________________________________________________
Перем _База Экспорт;
Перем Сервис;
Перем РасшФормы;

Перем ТаблицаОбъектов Экспорт;    //Таблица с объектами типа КнопкаActiveButton
Перем СписокВидимыхКнопок Экспорт;//Список видимых кнопок (на видимых слоях) отсортирован по вертикальной координате

Перем НомерТекущейКнопки Экспорт;
Перем КаталогКнопок;




Функция Этот(Конт) Возврат Конт; КонецФункции
Функция Сам() Возврат Этот(Контекст) КонецФункции




//======================================================================
Процедура Конструктор()
	_База = Этот(Контекст).ПолучитьБазовыйКласс("Объект");

	Сервис = СоздатьОбъект("Сервис");
	РасшФормы = СоздатьОбъект("РасширениеФормы");
	НомерТекущейКнопки=1;


	ТаблицаОбъектов=СоздатьОбъект("ТаблицаЗначений");

	ТаблицаОбъектов.НоваяКолонка("Атрибут");
	ТаблицаОбъектов.НоваяКолонка("Слой");
	ТаблицаОбъектов.НоваяКолонка("Кнопка");
	ТаблицаОбъектов.НоваяКолонка("Имя");
	ТаблицаОбъектов.НоваяКолонка("ВидОбъекта","Число");//Определяет вид объекта - кнопка, текст, и т.п
	ТаблицаОбъектов.НоваяКолонка("X");
	ТаблицаОбъектов.НоваяКолонка("Y");
	ТаблицаОбъектов.НоваяКолонка("Текущая","Число");


	СписокВидимыхКнопок=СоздатьОбъект("СписокЗначений");

	КаталогКнопок=Лев(КаталогИБ(),СтрДлина(КаталогИБ())-1);


КонецПроцедуры // Конструктор

Процедура УдалитьКнопки() Экспорт
	ТаблицаОбъектов.ВыбратьСтроки();
	Пока ТаблицаОбъектов.ПолучитьСтроку() = 1 Цикл
		Попытка
			ТаблицаОбъектов.Кнопка=0;
		Исключение
		КонецПопытки;

	КонецЦикла;
	ТаблицаОбъектов.УдалитьСтроки();
КонецПроцедуры

Процедура Деструктор()
	УдалитьКнопки();
КонецПроцедуры

Функция КаталогКнопок(пКаталогКнопок="") Экспорт
	Если пКаталогКнопок<>"" Тогда
		КаталогКнопок=пКаталогКнопок;
		Если ТаблицаОбъектов.КоличествоСтрок()>0 Тогда
			ТаблицаОбъектов.ВыбратьСтроки();
			Пока ТаблицаОбъектов.ПолучитьСтроку() = 1 Цикл
				ТаблицаОбъектов.Кнопка.КаталогКнопок=КаталогКнопок;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	Возврат КаталогКнопок;
КонецФункции


////======================================================================
//Функция ПеременнаяКонтекста(Идент)
//	Перем ПеремКонтекста;
//	Сам=Сам();
//	Сервис.ПолучитьПеременнуюКонтекста(Сам.КонтекстФормы,Идент,ПеремКонтекста);
//	Возврат ПеремКонтекста;
//КонецФункции //ПеременнаяКонтекста


Процедура УстановитьФокус(Идент) Экспорт


	ТаблицаОбъектов.ВыбратьСтроки();
	Пока ТаблицаОбъектов.ПолучитьСтроку() = 1 Цикл
		Если (ТаблицаОбъектов.Текущая=1)И(ВРЕГ(ТаблицаОбъектов.Имя)<>ВРЕГ(Идент)) Тогда
			ТаблицаОбъектов.Текущая=0;

			ТаблицаОбъектов.Кнопка.СнятьФокус();

		ИначеЕсли ВРЕГ(ТаблицаОбъектов.Имя)=ВРЕГ(Идент) Тогда
			ТаблицаОбъектов.Текущая=1;

			ТаблицаОбъектов.Кнопка.УстановитьФокус();
		КонецЕсли;
	КонецЦикла;


КонецПроцедуры //УстановитьФокус

//Слои - слои формы через запятую
//возвращает список кнопок - объектов ActiveX отсортированных по вертикали и горизонтали
//расположенных на перечисленных слоях
Функция ПолучитьСписокВидимыхКнопок(Слои) экспорт

	СписокКнопок=СоздатьОбъект("СписокЗначений");
	ТаблицаОбъектов.Сортировать("Y,X");
	СписокСлоев=СтрЗаменить(ВРЕГ(Слои),",",РазделительСтрок);

	ТаблицаОбъектов.ВыбратьСтроки();
	Пока ТаблицаОбъектов.ПолучитьСтроку()=1 Цикл
		Если ТаблицаОбъектов.ВидОбъекта=0 Тогда
			СлойКнопки=ВРЕГ(ТаблицаОбъектов.Слой);

			Для Инд=1 По СтрКоличествоСтрок(СписокСлоев) Цикл
				Если СтрПолучитьСтроку(СписокСлоев,Инд)=СлойКнопки Тогда
					СписокКнопок.ДобавитьЗначение(ТаблицаОбъектов.Кнопка,ТаблицаОбъектов.Имя);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;

	Если НомерТекущейКнопки>СписокКнопок.РазмерСписка() Тогда
		НомерТекущейКнопки=СписокКнопок.РазмерСписка();
	ИначеЕсли (НомерТекущейКнопки=0)И(СписокКнопок.РазмерСписка()>0) Тогда
		НомерТекущейКнопки=1;
	КонецЕсли;

	Возврат СписокКнопок;
КонецФункции


Процедура АвтоРисование(Режим=1) Экспорт
	ТаблицаОбъектов.ВыбратьСтроки();
	Пока ТаблицаОбъектов.ПолучитьСтроку() = 1 Цикл
		ТаблицаОбъектов.Кнопка.АвтоРисование(Режим);
	КонецЦикла;

КонецПроцедуры

Функция ПолучитьОбъектПоИмени(ИмяОбъекта) Экспорт
Перем Стр;
	Стр=0;
	Если ТаблицаОбъектов.НайтиЗначение(ИмяОбъекта,Стр,"Имя")>0 Тогда
		ТаблицаОбъектов.ПолучитьСтрокуПоНомеру(Стр);
		Возврат ТаблицаОбъектов.Кнопка;
	КонецЕсли;
	Возврат "";
КонецФункции


Процедура УстановитьВидОбъекта(ИмяОбъекта,ВидОбъекта) Экспорт
Перем Стр;
	Стр=0;
	Если ТаблицаОбъектов.НайтиЗначение(ИмяОбъекта,Стр,"Имя")>0 Тогда
		ТаблицаОбъектов.ПолучитьСтрокуПоНомеру(Стр);
		Если ВидОбъекта=100 Тогда
			//Это кнопка, но ее не следует разрешать выбирать по Tab и стрелками
			ТаблицаОбъектов.Кнопка.Объект.Style=0;
		Иначе
			ТаблицаОбъектов.Кнопка.Объект.Style=ВидОбъекта;
		КонецЕсли;
		ТаблицаОбъектов.ВидОбъекта=ВидОбъекта;
	КонецЕсли;

КонецПроцедуры



Функция ТекущаяКнопка(ИмяКнопки="") Экспорт
	Если (НомерТекущейКнопки>СписокВидимыхКнопок.РазмерСписка())Или(НомерТекущейКнопки=0) Тогда
		НомерТекущейКнопки=СписокВидимыхКнопок.РазмерСписка();
	КонецЕсли;
	Если НомерТекущейКнопки=0 Тогда
		Возврат "";
	КонецЕсли;
	Кнопка=СписокВидимыхКнопок.ПолучитьЗначение(НомерТекущейКнопки,ИмяКнопки);
	Возврат Кнопка;
КонецФункции

Функция ПодсказкаТекущейКнопки() Экспорт
	Кнопка=ТекущаяКнопка();
	возврат Кнопка.ОписаниеЭлемента;
КонецФункции


Процедура УстановитьФокусПоНомеру(НомерКнопки) Экспорт

	Если НомерКнопки>СписокВидимыхКнопок.РазмерСписка() Тогда
		НомерТекущейКнопки=1;
	ИначеЕсли НомерКнопки<=0 Тогда
		НомерТекущейКнопки=СписокВидимыхКнопок.РазмерСписка();
	Иначе
		НомерТекущейКнопки=НомерКнопки;
	КонецЕсли;

	Если НомерКнопки>0 Тогда
		Идент="";
		СписокВидимыхКнопок.ПолучитьЗначение(НомерТекущейКнопки,Идент);
		УстановитьФокус(Идент);
	КонецЕсли;

КонецПроцедуры

//выделить первую кнопку
Процедура Первая() Экспорт
	УстановитьФокусПоНомеру(1);
КонецПроцедуры

Процедура Последняя() Экспорт
	УстановитьФокусПоНомеру(СписокВидимыхКнопок.РазмерСписка());
КонецПроцедуры


Процедура Следующая() Экспорт
	Если НомерТекущейКнопки<СписокВидимыхКнопок.РазмерСписка() Тогда
		НомерТекущейКнопки=НомерТекущейКнопки+1;
	Иначе
		НомерТекущейКнопки=1;
	КонецЕсли;
	УстановитьФокусПоНомеру(НомерТекущейКнопки);
КонецПроцедуры

Процедура Предыдущая() Экспорт
	Если НомерТекущейКнопки>1 Тогда
		НомерТекущейКнопки=НомерТекущейКнопки-1;
	Иначе
		НомерТекущейКнопки=СписокВидимыхКнопок.РазмерСписка();
	КонецЕсли;

	УстановитьФокусПоНомеру(НомерТекущейКнопки);
КонецПроцедуры

Процедура Вниз() Экспорт
	Сам=Сам();
	Сам.Следующая();
КонецПроцедуры

Процедура Вверх() Экспорт
	Сам=Сам();
	Сам.Предыдущая();
КонецПроцедуры


Процедура ИнициализироватьВидимыеКнопки(Слои="Основной",УстановитьНомерТекущейКнопки=0) Экспорт
	СписокВидимыхКнопок=ПолучитьСписокВидимыхКнопок(Слои);
	Если УстановитьНомерТекущейКнопки>0 Тогда
		УстановитьФокусПоНомеру(УстановитьНомерТекущейКнопки);
	Иначе
		Первая();
	КонецЕсли;
	Для Инд=1 По СписокВидимыхКнопок.РазмерСписка() Цикл
		Кнопка=СписокВидимыхКнопок.ПолучитьЗначение(Инд);
		Кнопка.Авторисование(1);
		Кнопка.Нарисовать();
	КонецЦикла;
КонецПроцедуры



//======================================================================
Процедура УстановитьКонтекст(Конт) Экспорт
	_База.УстановитьКонтекст(Конт);
	РасшФормы.УстановитьФорму(Конт.Форма);

	Для Х = 0 По РасшФормы.КоличествоАтрибутов() - 1 Цикл
		ТипАтрибута=РасшФормы.ПолучитьАтрибут(Х).Тип;
		Если (ТипАтрибута = 1 )Или(ТипАтрибута = 3)
		Тогда
			Атрибут=РасшФормы.ПолучитьАтрибут(Х);
		    Идент = Атрибут.Идентификатор;
			Описание=Атрибут.Описание;
			Если ПустаяСтрока(Описание) = 1 Тогда
				Продолжить;
			Иначе
				ИД=СтрПолучитьСтроку(Описание,1);
				Если ИД="3D_Active_Button" Тогда
					АктивБатон=СоздатьОбъект("КнопкаActiveButton");
					АктивБатон.КаталогКнопок=КаталогКнопок;
					АктивБатон.УстановитьАтрибут(Конт,Идент);

					Если ПустаяСтрока(Атрибут.Заголовок)=0 Тогда
						АктивБатон.УстановитьТекст(Атрибут.Заголовок);
					КонецЕсли;




					Стр=0;
					Если ТаблицаОбъектов.НайтиЗначение(Идент,Стр,"Имя")=0 Тогда
						ТаблицаОбъектов.НоваяСтрока();
					Иначе
						ТаблицаОбъектов.ПолучитьСтрокуПоНомеру(Стр);
					КонецЕсли;


					ТаблицаОбъектов.Атрибут = Атрибут;
					ТаблицаОбъектов.Слой= Атрибут.Слой;
					ТаблицаОбъектов.Кнопка = АктивБатон;
					ТаблицаОбъектов.Имя = Идент;

					ТаблицаОбъектов.X = АктивБатон.Лево;
					ТаблицаОбъектов.Y = АктивБатон.Верх;
					ТаблицаОбъектов.ВидОбъекта=АктивБатон.Активикс.Объект.Style;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры //ВыбратьКнопки

Функция ВнешнееСобытие(Издатель,Событие) Экспорт
	Сам=Сам();

	Отработали=0;


	Если Событие.Источник="Команда" Тогда

		Команда=Событие.Описатель.Команда;

		Если Команда="ВНИЗ" Тогда
			Сам.Вниз();
			Отработали = 1;
		ИначеЕсли Команда="ВВЕРХ" Тогда
			Сам.Вверх();
			Отработали = 1;
		ИначеЕсли Команда="ВПРАВО" Тогда
			Сам.Следующая();
			Отработали = 1;
		ИначеЕсли Команда="ВЛЕВО" Тогда
			Сам.Предыдущая();
			Отработали = 1;
		ИначеЕсли Команда="В_НАЧАЛО" Тогда
			Сам.Первая();
			Отработали = 1;
		ИначеЕсли Команда="В_КОНЕЦ" Тогда
			Сам.Последняя();
			Отработали = 1;
		КонецЕсли;
	КонецЕсли;

	Если Отработали = 1 Тогда
		глСобытиеОбработано(Событие);
		глПослатьКоманду(Сам,"Обновить");
		Возврат 0;
    КонецЕсли;
	Возврат 1;
КонецФункции
