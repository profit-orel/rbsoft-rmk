//Конфигурация для платформы "1С:Предприятие 7.7" РБ-Софт:Рабочее место кассира.
//Версия 4.57 от 05.06.11 23:24:52
//(c) Баинов Евгений, ООО "РБ-Софт" 2006-2011 г.Улан-Удэ, ул.Балтахинова,17Е,офис 215
//e-mail: bainov@rb-soft.ru  icq: 73718937, телефоны: (3012) 440-357, 278-228
//Сайт проекта: http://code.google.com/p/rbsoft-rmk/
//_______________________________________________________________________________________
//Экранная форма класса Отчеты.УниверсальныйОтчет
//Имя формы : ОсновнаяФорма
//_______________________________________________________________________________________
Перем ПоКнопке;
Перем ПолеТабличногоДокумента;
Перем Объект;
Перем ПродавецСтр,НомерКассовогоМеста;
Перем СчетчикПоказателей;
Перем МассивКолонок[4];	


Процедура ПриЗакрытии()
	Если ПоКнопке=0 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры


Процедура Печатать()
	ПолеТабличногоДокумента.Таблица.Напечатать(1);
КонецПроцедуры


Процедура ВывестиПустышку()



	ПолеТабличногоДокумента.Таблица.ИсходнаяТаблица("Заставка");
	ПолеТабличногоДокумента.Таблица.Очистить();
	ПолеТабличногоДокумента.Таблица.Вывести();

	ПолеТабличногоДокумента.Таблица.ТолькоПросмотр(1);
	ПолеТабличногоДокумента.Таблица.ПараметрыСтраницы(1,,,10,0,0,0,0,0,1);
	ПолеТабличногоДокумента.Таблица.Опции(0,0,0,0);

	ПолеТабличногоДокумента.Менеджер.ВидимостьПолосПрокрутки(0);
	ПолеТабличногоДокумента.Менеджер.ВыводСетки=0;
	ПолеТабличногоДокумента.Менеджер.ВыводЗаголовков=0;
	ПолеТабличногоДокумента.Менеджер.Масштаб(120);


КонецПроцедуры

Процедура ПрименитьКраткиеНастройки()
	Объект.СтруктураОтбора.ДатаНачала.Значение=ДатаНачала;
	Если ПустоеЗначение(ДатаНачала)=1 Тогда
		Объект.СтруктураОтбора.ДатаНачала.Использовать=0;
	Иначе
		Объект.СтруктураОтбора.ДатаНачала.Использовать=1;
		Объект.СтруктураОтбора.ДатаНачала.Значение=ДатаНачала;
	КонецЕсли;


	Объект.СтруктураОтбора.ДатаКонца.Значение=ДатаКонца;
	Если ПустоеЗначение(ДатаКонца)=1 Тогда
		Объект.СтруктураОтбора.ДатаКонца.Использовать=0;
	Иначе
		Объект.СтруктураОтбора.ДатаКонца.Использовать=1;
		Объект.СтруктураОтбора.ДатаКонца.Значение=ДатаКонца;
	КонецЕсли;

КонецПроцедуры


Процедура ВывестиОтчет(Таблица,ТЗ,Уровень=1)

	Секция="Г"+Уровень;
	
	
	ПолеГруппировки=Объект.ПолучитьПолеГруппировки(Уровень);
	ИмяПоля=ПолеГруппировки.ИмяПоляТаблицы;
	
	Если ИмяПоля="GoodID" Тогда
		ТЗ.Сортировать("Name");
	ИначеЕсли ИмяПоля="Date" Тогда
		ТЗ.Сортировать("Date");
	ИначеЕсли ИмяПоля="SellerID" Тогда
		ТЗ.Сортировать("SellerName");
	КонецЕсли;	
	
	ТЗ.ВыбратьСтроки();
	Пока ТЗ.ПолучитьСтроку()=1 Цикл
		Группировка=ТЗ.ПолучитьЗначение(,ИмяПоля);
		Количество=глФРМКоличество(ТЗ.ПолучитьЗначение(,"TotalCount"));
		Сумма	=глФРМ(ТЗ.ПолучитьЗначение(,"Summ"));
		Скидка	=глФРМ(ТЗ.ПолучитьЗначение(,"Discount"));
		Итого	=глФРМ(ТЗ.ПолучитьЗначение(,"Total"));
		
		Если ИмяПоля="GoodID" Тогда
			Группировка=ТЗ.ПолучитьЗначение(,"Name");
			Цена=глФРМ(ТЗ.ПолучитьЗначение(,"Price"));
		Иначе
			Цена="";
			
			Если ИмяПоля="ID" Тогда
				Группировка=ТЗ.ПолучитьЗначение(,"Time")+" чек № "+ТЗ.ПолучитьЗначение(,"NumCheck");
			ИначеЕсли ИмяПоля="Z" Тогда
				Группировка="Смена № "+ТЗ.ПолучитьЗначение(,"Z");
			ИначеЕсли ИмяПоля="Date" Тогда
				Группировка=глСтроки.ДатаИзСтроки(ТЗ.ПолучитьЗначение(,"Date"));
			ИначеЕсли ИмяПоля="SN" Тогда
				Группировка="ККМ № "+ТЗ.ПолучитьЗначение(,"SN");
			ИначеЕсли ИмяПоля="SellerID" Тогда
				Группировка=ТЗ.ПолучитьЗначение(,"SellerName");
			
			КонецЕсли;
			
		КонецЕсли;	
		
		Таблица.ВывестиСекцию(Секция+"|Группировка");

		Для Инд=1 По СчетчикПоказателей Цикл
			Таблица.ПрисоединитьСекцию(Секция+"|"+МассивКолонок[Инд]);
		КонецЦикла;	
				
		Если ТЗ.ИмяКолонки(ТЗ.КоличествоКолонок())="тзПотомки" Тогда
			ВывестиОтчет(Таблица,ТЗ.ТЗПотомки,Уровень+1)
		КонецЕсли;
	КонецЦикла;
	
	Если Уровень=1 Тогда
		Количество=глФРМКоличество(ТЗ.Итог("TotalCount"));
		Сумма	=глФРМ(ТЗ.Итог("Summ"));
		Скидка	=глФРМ(ТЗ.Итог("Discount"));
		Итого	=глФРМ(ТЗ.Итог("Total"));
		
		Таблица.ВывестиСекцию("Итог|Группировка");
		Для Инд=1 По СчетчикПоказателей Цикл
			Таблица.ПрисоединитьСекцию("Итог|"+МассивКолонок[Инд]);
		КонецЦикла;	
	
	КонецЕсли;
КонецПроцедуры


Процедура Сформировать()

	Если (ПустоеЗначение(ДатаНачала)=0)И(ПустоеЗначение(ДатаКонца)=0)И(ДатаКонца<ДатаНачала) Тогда

		глПредупреждение("Некорректный период отчета");
		Возврат;
	КонецЕсли;

	ПрименитьКраткиеНастройки();
	
	
	Если Объект.СтруктураОтбора.Продавец.Использовать=0 Тогда
		ПродавецСтр="Все";
	Иначе
		
		Если ПустоеЗначение(Объект.СтруктураОтбора.Продавец.Значение)=1 Тогда
			ПродавецСтр="Пусто";
		Иначе
			ПродавецСтр=Объект.СтруктураОтбора.Продавец.Значение;
		КонецЕсли;
		ПродавецСтр=Объект.СтруктураОтбора.Продавец.ТипСравнения+" "+ПродавецСтр;
	КонецЕсли;
	
	
	ТЗРезультат=Объект.Выполнить();
	
	Если ПустоеЗначение(ТЗРезультат)=1 Тогда
		возврат;
	КонецЕсли;
	
	
	Группировки=Объект.ЗаголовокГруппировок;	
	
	ПолеТабличногоДокумента.Таблица.ИсходнаяТаблица("ОтчетПоПродавцам");
	ПолеТабличногоДокумента.Таблица.Очистить();
	
	ПолеТабличногоДокумента.Менеджер.ВидимостьПолосПрокрутки(1);
	ПолеТабличногоДокумента.Менеджер.ВыводСетки=0;
	ПолеТабличногоДокумента.Менеджер.ВыводЗаголовков=0;
	ПолеТабличногоДокумента.Менеджер.Масштаб(120);

	СчетчикПоказателей=0;
		
	Для Инд=1 По Объект.СтруктураПоказателей.Количество() Цикл
		ПолеПоказателя=Объект.СтруктураПоказателей.Получить(Инд);
		Если ПолеПоказателя.Использовать=1 Тогда
			СчетчикПоказателей=СчетчикПоказателей+1;
			МассивКолонок[СчетчикПоказателей]=ПолеПоказателя.Имя;
		КонецЕсли;	
	КонецЦикла;
	
	ПолеТабличногоДокумента.Таблица.ВывестиСекцию("Шапка|Группировка");
	
	Для Инд=1 По СчетчикПоказателей Цикл
		ПолеТабличногоДокумента.Таблица.ПрисоединитьСекцию("Шапка|"+МассивКолонок[Инд]);
	КонецЦикла;	
	
	
	ПолеТабличногоДокумента.Таблица.ТолькоПросмотр(1);
	ПолеТабличногоДокумента.Таблица.ПараметрыСтраницы(1,,,10,0,0,0,0,0,1);
	ПолеТабличногоДокумента.Таблица.Опции(0,0,ПолеТабличногоДокумента.Таблица.ВысотаТаблицы());
	

	ВывестиОтчет(ПолеТабличногоДокумента.Таблица,ТЗРезультат);

	ПолеТабличногоДокумента.Менеджер.Обновить();
	
КонецПроцедуры


Процедура ОбновитьПоля()
	Если Объект.СтруктураОтбора.ДатаНачала.Использовать=1 Тогда
		ДатаНачала=Объект.СтруктураОтбора.ДатаНачала.Значение;
	Иначе
		ДатаНачала="";
	КонецЕсли;
	Если Объект.СтруктураОтбора.ДатаКонца.Использовать=1 Тогда
		ДатаКонца=Объект.СтруктураОтбора.ДатаКонца.Значение;
	Иначе
		ДатаКонца="";

	КонецЕсли;


КонецПроцедуры

Процедура ПриОткрытии()

	Если ПустоеЗначение(Форма.Параметр)=1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	СтруктураПараметров=Форма.Параметр;
	Объект=СтруктураПараметров.Объект;

	ОбновитьПоля();
КонецПроцедуры	// ПриОткрытии


Процедура ПослеОткрытия()


	оФорма = СоздатьОбъект("Форма");
	оФорма.УстановитьФорму(Форма);

	ПолеТабличногоДокумента=оФорма.СоздатьЭлементУправления("ПолеТабличногоДокумента",Форма.ПолеТаблицы);
	
	ВывестиПустышку();

КонецПроцедуры



Процедура Настройка()
	ПрименитьКраткиеНастройки();
	Результат=Объект.ОткрытьФормуОбъектаМодально("ФормаНастройки");
	ОбновитьПоля();
	
	Если Результат=1 Тогда
		Сформировать();
	КонецЕсли;
КонецПроцедуры


ПоКнопке=0;