//Конфигурация для платформы "1С:Предприятие 7.7" РБ-Софт:Рабочее место кассира.
//Версия 4.57 от 05.06.11 23:24:52
//(c) Баинов Евгений, ООО "РБ-Софт" 2006-2011 г.Улан-Удэ, ул.Балтахинова,17Е,офис 215
//e-mail: bainov@rb-soft.ru  icq: 73718937, телефоны: (3012) 440-357, 278-228
//Сайт проекта: http://code.google.com/p/rbsoft-rmk/
//_______________________________________________________________________________________
//Класс ТабличноеПолеСНастройками
//_______________________________________________________________________________________
Перем _База Экспорт;
Перем КонтекстВызывающейФормы Экспорт; //:ГрупповойКонтекст
Перем ИдентификаторРеквизита Экспорт;

Перем фМожноВводитьНовыеЭлементы Экспорт;//1- можно вводить новые

Функция Этот(Конт) Возврат Конт; КонецФункции
Функция Сам() Возврат Этот(Контекст) КонецФункции

Функция _ПолучитьКод() экспорт
	возврат "ТабличноеПолеСНастройками";
КонецФункции

Функция Вид() Экспорт
	Возврат ТипЗначенияСтр(Сам());
КонецФункции

Функция Инит(КонтекстФормы, _ИдентификаторРеквизита) Экспорт
	Сам = Сам();
	Сам.КонтекстВызывающейФормы=глВзятьКонтекст(КонтекстФормы);

	ИдентификаторРеквизита=_ИдентификаторРеквизита;

	оФорма = СоздатьОбъект("Форма");
	оФорма.УстановитьФорму(КонтекстФормы.Форма);
	оФорма.СоздатьЭлементУправления(Сам, ИдентификаторРеквизита);

	Возврат 1;
КонецФункции

Процедура ВыполнитьВыбор(Объект,ДопДанные="") Экспорт
	Сервис=СоздатьОбъект("Сервис");
	СписокАргументов=СоздатьОбъект("СписокЗначений");
	СписокАргументов.ДобавитьЗначение(Объект);
	СписокАргументов.ДобавитьЗначение(ДопДанные);
	Сервис.ВыполнитьПроцедуру(КонтекстВызывающейФормы, ИдентификаторРеквизита+"_ВыполнитьВыбор", СписокАргументов);
КонецПроцедуры

Процедура Конструктор()
	_База = Сам().ПолучитьБазовыйКласс("ТабличноеПоле");
	фМожноВводитьНовыеЭлементы=0;
КонецПроцедуры	// Конструктор



Функция тзТекущиеНастройки() Экспорт
	Колонки = Сам().Колонки;
	тзНастройки = СоздатьОбъект("ТаблицаЗначений");
	тзНастройки.НоваяКолонка("ИмяКолонки", "Строка");
	тзНастройки.НоваяКолонка("Видимость", "Число");
	тзНастройки.НоваяКолонка("Ширина", "Число");
	тзНастройки.НоваяКолонка("Сортировка", "Число");

	Для н = 1 По Колонки.Количество() Цикл
		Колонка = Колонки.Получить(н-1);
		тзНастройки.НоваяСтрока();
		тзНастройки.ИмяКолонки = Колонка.Имя;
		тзНастройки.Видимость = Колонка.Видимость;
		тзНастройки.Ширина = Колонка.Ширина;
		тзНастройки.Сортировка = Колонка.ЗнакУпорядочивания;
	КонецЦикла;

	Возврат тзНастройки;
КонецФункции


Процедура ВводНовыхЭлементов(пМожноВводитьНовыеЭлементы) Экспорт
	фМожноВводитьНовыеЭлементы=пМожноВводитьНовыеЭлементы;
КонецПроцедуры


Процедура ВосстановитьНастройки(тзНастройки) Экспорт
	Колонки = Сам().Колонки;
	КоличествоКолонок = Колонки.Количество();
	тзНастройки.ВыбратьСтроки();
	Пока тзНастройки.ПолучитьСтроку() = 1 Цикл
		Попытка
			Колонка = Колонки.Получить(тзНастройки.ИмяКолонки);
		Исключение
			Продолжить;
		КонецПопытки;

		Попытка
			Колонка.Ширина = тзНастройки.Ширина;
		Исключение
		КонецПопытки;

		Попытка
			Колонка.Видимость = тзНастройки.Видимость;
		Исключение
		КонецПопытки;

		Если тзНастройки.НомерСтроки <= КоличествоКолонок Тогда
			чИндекс = Колонки.Индекс(Колонка);
			Если тзНастройки.НомерСтроки <> (чИндекс+1) Тогда
				Колонки.Сдвинуть(чИндекс, тзНастройки.НомерСтроки - (чИндекс+1));
			КонецЕсли;
		КонецЕсли;

		Попытка
			Если тзНастройки.Сортировка <> 0 Тогда
				Если тзНастройки.Сортировка = 1 Тогда
					Сам().УстановитьСортировку(тзНастройки.ИмяКолонки, 1);
				Иначе
					Сам().УстановитьСортировку(тзНастройки.ИмяКолонки, -1);
				КонецЕсли;
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры


Процедура ПриАктивизацииСтроки() Экспорт
	Сам=Сам();
	Сервис=СоздатьОбъект("Сервис");
	СписокАргументов=СоздатьОбъект("СписокЗначений");
	СписокАргументов.ДобавитьЗначение(Сам);
	СписокАргументов.ДобавитьЗначение(Сам.ТекущаяКолонка);
	СписокАргументов.ДобавитьЗначение(Сам.ТекущиеДанные);
	Сервис.ВыполнитьПроцедуру(КонтекстВызывающейФормы, ИдентификаторРеквизита+"_ПриАктивизацииСтроки", СписокАргументов);
КонецПроцедуры


Процедура ПриАктивизацииКолонки() Экспорт
	Сам=Сам();
	Сервис=СоздатьОбъект("Сервис");
	СписокАргументов=СоздатьОбъект("СписокЗначений");
	СписокАргументов.ДобавитьЗначение(Сам);
	СписокАргументов.ДобавитьЗначение(Сам.ТекущаяКолонка);
	СписокАргументов.ДобавитьЗначение(Сам.ТекущиеДанные);
	Сервис.ВыполнитьПроцедуру(КонтекстВызывающейФормы, ИдентификаторРеквизита+"_ПриАктивизацииКолонки", СписокАргументов);
КонецПроцедуры


Процедура ПриАктивизацииЯчейки() Экспорт
	Сам=Сам();
	Сервис=СоздатьОбъект("Сервис");
	СписокАргументов=СоздатьОбъект("СписокЗначений");
	СписокАргументов.ДобавитьЗначение(Сам);
	СписокАргументов.ДобавитьЗначение(Сам.ТекущаяКолонка);
	СписокАргументов.ДобавитьЗначение(Сам.ТекущиеДанные);
	Сервис.ВыполнитьПроцедуру(КонтекстВызывающейФормы, ИдентификаторРеквизита+"_ПриАктивизацииЯчейки", СписокАргументов);
КонецПроцедуры


Процедура ПриВыводеСтроки(ОформлениеСтроки, ДанныеСтроки) Экспорт

	Сам=Сам();
	Сервис=СоздатьОбъект("Сервис");
	СписокАргументов=СоздатьОбъект("СписокЗначений");
	СписокАргументов.ДобавитьЗначение(Сам);
	СписокАргументов.ДобавитьЗначение(Сам.ТекущаяКолонка);
	СписокАргументов.ДобавитьЗначение(ДанныеСтроки);
	СписокАргументов.ДобавитьЗначение(ОформлениеСтроки);
	Сервис.ВыполнитьПроцедуру(КонтекстВызывающейФормы, ИдентификаторРеквизита+"_ПриВыводеСтроки", СписокАргументов);
КонецПроцедуры

Функция КоличествоСтрокНаСтранице() Экспорт
Перем чЛево, чВерх, чПраво, чНиз, тпЛево, тпВерх, тпШрирна, тпВысота;	
	Сам=Сам();
	
	
	Сам.ПолучитьГраницыТекущейЯчейки(чЛево, чВерх, чПраво, чНиз,1);
	ВысотаЯчейки=чНиз-чВерх+1;
	
	Если ВысотаЯчейки=0 Тогда
		возврат 0;
	КонецЕсли;
	РасширениеФормы=СоздатьОбъект("РасширениеФормы");
	РасширениеФормы.УстановитьФорму(Сам.КонтекстВызывающейФормы.Форма);
	АтрибутТП=РасширениеФормы.ПолучитьАтрибут(ИдентификаторРеквизита);
	АтрибутТП.ПолучитьКоординаты(тпЛево, тпВерх, тпШрирна, тпВысота);
	
	возврат Цел(тпВысота/ВысотаЯчейки);
КонецФункции

Процедура ВНачало() Экспорт
	Сам=Сам();
	Если Сам.ТекущаяСтрока>1 Тогда
		Сам.ТекущаяСтрока=1;
	КонецЕсли;	
КонецПроцедуры

Процедура ВКонец() Экспорт
	Сам=Сам();
	Если Сам.ТекущаяСтрока<Сам.ПоставщикДанных.ТаблицаЗначений.КоличествоСтрок() Тогда
		Сам.ТекущаяСтрока=Сам.ПоставщикДанных.ТаблицаЗначений.КоличествоСтрок();
	КонецЕсли;	
КонецПроцедуры

Процедура СтраницаВверх() Экспорт
	Сам=Сам();
	СтрокНаСтранице=Сам.КоличествоСтрокНаСтранице();
	Если Сам.ТекущаяСтрока>СтрокНаСтранице Тогда
		Сам.ТекущаяСтрока=Сам.ТекущаяСтрока-СтрокНаСтранице;	
	Иначе
		Сам.ТекущаяСтрока=1;
	КонецЕсли;	
КонецПроцедуры

Процедура СтраницаВниз() Экспорт
	Сам=Сам();
	СтрокНаСтранице=Сам.КоличествоСтрокНаСтранице();
	
	Если Сам.ТекущаяСтрока+СтрокНаСтранице<Сам.ПоставщикДанных.ТаблицаЗначений.КоличествоСтрок() Тогда
		Сам.ТекущаяСтрока=Сам.ТекущаяСтрока+СтрокНаСтранице;	
	Иначе
		Сам.ТекущаяСтрока=Сам.ПоставщикДанных.ТаблицаЗначений.КоличествоСтрок();	
	КонецЕсли;	
КонецПроцедуры

Процедура АктивизироватьСледующий() Экспорт
	Сам=Сам();
	
	Если Сам.ТекущаяСтрока<Сам.ПоставщикДанных.ТаблицаЗначений.КоличествоСтрок() Тогда
		Сам.ТекущаяСтрока=Сам.ТекущаяСтрока+1;	
	КонецЕсли;	
	
КонецПроцедуры

Процедура АктивизироватьПредыдущий() Экспорт
	Сам=Сам();
	
	Если Сам.ТекущаяСтрока>1 Тогда
		Сам.ТекущаяСтрока=Сам.ТекущаяСтрока-1;
	КонецЕсли;	
	
КонецПроцедуры


Процедура ПриОткрытии()
	//#if _NOW_PREPARE_CLASS
	Форма.Параметр._ПриОткрытии(); // для отладки класса
	//#endif // _NOW_PREPARE_CLASS
КонецПроцедуры	// ПриОткрытии


